/-
Copyright (c) 2022 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/

import category_theory.localization.construction
import for_mathlib.category_theory.morphism_property_misc

noncomputable theory

open category_theory category_theory.category

variables {C : Type*} [category C] (W : morphism_property C)
variables {D : Type*} [category D]

namespace category_theory

variables (D)

@[derive category]
def morphism_property.functors_inverting := full_subcategory (Œª (F : C ‚•§ D), W.is_inverted_by F)

variables {D W}

def morphism_property.functors_inverting.mk (F : C ‚•§ D) (hF : W.is_inverted_by F) :
  W.functors_inverting D := ‚ü®F, hF‚ü©

variables (D W)
namespace morphism_property

lemma Q_inverts : W.is_inverted_by W.Q :=
Œª X Y w hw, is_iso.of_iso (localization.construction.Wiso w hw)

end morphism_property

namespace localization

namespace construction

lemma nat_trans_hcomp_injective {F G : W.localization ‚•§ D} (œÑ‚ÇÅ œÑ‚ÇÇ : F ‚ü∂ G)
  (h : ùüô W.Q ‚ó´ œÑ‚ÇÅ = ùüô W.Q ‚ó´ œÑ‚ÇÇ) : œÑ‚ÇÅ = œÑ‚ÇÇ :=
begin
  ext X,
  have eq := (obj_equiv W).right_inv X,
  simp only [obj_equiv] at eq,
  rw [‚Üê eq, ‚Üê nat_trans.id_hcomp_app, ‚Üê nat_trans.id_hcomp_app, h],
end

namespace whiskering_left_equivalence

@[simps]
def functor : (W.localization ‚•§ D) ‚•§ (W.functors_inverting D) :=
full_subcategory.lift _ ((whiskering_left _ _ D).obj W.Q)
  (Œª F, morphism_property.is_inverted_by.of_comp W W.Q W.Q_inverts _)

@[simps]
def inverse : (W.functors_inverting D) ‚•§ (W.localization ‚•§ D) :=
{ obj := Œª G, lift G.obj G.property,
  map := Œª G‚ÇÅ G‚ÇÇ œÑ, nat_trans_extension (eq_to_hom (by rw fac) ‚â´ œÑ ‚â´ eq_to_hom (by rw fac)),
  map_id' := Œª G, nat_trans_hcomp_injective _ _ _ _ begin
    rw nat_trans_extension_hcomp,
    ext X,
    simpa only [nat_trans.comp_app, eq_to_hom_app, eq_to_hom_refl, comp_id, id_comp,
      nat_trans.hcomp_id_app, nat_trans.id_app, functor.map_id],
  end,
  map_comp' := Œª G‚ÇÅ G‚ÇÇ G‚ÇÉ œÑ‚ÇÅ œÑ‚ÇÇ, nat_trans_hcomp_injective _ _ _ _ begin
    ext X,
    simpa only [nat_trans_extension_hcomp, nat_trans.comp_app, eq_to_hom_app, eq_to_hom_refl,
      id_comp, comp_id, nat_trans.hcomp_app, nat_trans.id_app, functor.map_id,
      nat_trans_extension_app, nat_trans_extension.app_eq],
  end, }

@[simps]
lemma unit_iso : ùü≠ (W.localization ‚•§ D) ‚âÖ functor W D ‚ãô inverse W D := eq_to_iso
begin
  refine functor.ext (Œª G, _) (Œª G‚ÇÅ G‚ÇÇ œÑ, _),
  { apply uniq,
    dsimp [functor],
    rw fac, },
  { apply nat_trans_hcomp_injective,
    ext X,
    simp only [functor.id_map, nat_trans.hcomp_app, comp_id, functor.comp_map,
      inverse_map, nat_trans.comp_app, eq_to_hom_app, eq_to_hom_refl, nat_trans_extension_app,
      nat_trans_extension.app_eq, functor_map_app, id_comp], },
end

@[simps]
lemma counit_iso : inverse W D ‚ãô functor W D ‚âÖ ùü≠ (W.functors_inverting D) := eq_to_iso
begin
  refine functor.ext _ _,
  { rintro ‚ü®G, hG‚ü©,
    ext1,
    apply fac, },
  { rintros ‚ü®G‚ÇÅ, hG‚ÇÅ‚ü© ‚ü®G‚ÇÇ, hG‚ÇÇ‚ü© f,
    ext X,
    apply nat_trans_extension.app_eq, },
end

end whiskering_left_equivalence

def whiskering_left_equivalence : (W.localization ‚•§ D) ‚âå W.functors_inverting D :=
{ functor := whiskering_left_equivalence.functor W D,
  inverse := whiskering_left_equivalence.inverse W D,
  unit_iso := whiskering_left_equivalence.unit_iso W D,
  counit_iso := whiskering_left_equivalence.counit_iso W D,
  functor_unit_iso_comp' := Œª F, begin
    ext X,
    simpa only [eq_to_hom_app, whiskering_left_equivalence.unit_iso_hom,
      whiskering_left_equivalence.counit_iso_hom, eq_to_hom_map, eq_to_hom_trans,
      eq_to_hom_refl],
  end, }

end construction

end localization

end category_theory
