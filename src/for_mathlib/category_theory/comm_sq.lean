/-
Copyright (c) 2022 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison
-/
import category_theory.limits.shapes.pullbacks
import category_theory.limits.opposites
import category_theory.limits.shapes.zero_morphisms
import for_mathlib.category_theory.limits.opposites_pullbacks

/-!
# Pullback and pushout squares

We provide another API for pullbacks and pushouts.

`is_pullback fst snd f g` is the proposition that
```
  P --fst--> X
  |          |
 snd         f
  |          |
  v          v
  Y ---g---> Z

```
is a pullback square.

(And similarly for `is_pushout`.)

We provide the glue to go back and forth to the usual `is_limit` API for pullbacks, and prove
`is_pullback (pullback.fst : pullback f g ‚ü∂ X) (pullback.snd : pullback f g ‚ü∂ Y) f g`
for the usual `pullback f g` provided by the `has_limit` API.

We don't attempt to restate everything we know about pullbacks in this language,
but do restate the pasting lemmas.

## Future work
Bicartesian squares, and
show that the pullback and pushout squares for a biproduct are bicartesian.
-/

open category_theory

namespace category_theory.limits

variables {C : Type*} [category C]

/-- The proposition that a square
```
  W ---f---> X
  |          |
  g          h
  |          |
  v          v
  Y ---i---> Z

```
is a commuting square.
-/
structure comm_sq {W X Y Z : C} (f : W ‚ü∂ X) (g : W ‚ü∂ Y) (h : X ‚ü∂ Z) (i : Y ‚ü∂ Z) : Prop :=
(w : f ‚â´ h = g ‚â´ i)

attribute [reassoc] comm_sq.w

namespace comm_sq

lemma flip {W X Y Z : C} {f : W ‚ü∂ X} {g : W ‚ü∂ Y} {h : X ‚ü∂ Z} {i : Y ‚ü∂ Z}
  (p : comm_sq f g h i) : comm_sq g f i h := ‚ü®p.w.symm‚ü©

lemma of_arrow {f g : arrow C} (h : f ‚ü∂ g) : comm_sq f.hom h.left h.right g.hom := ‚ü®h.w.symm‚ü©

@[simps]
def op {W X Y Z : C} {f : W ‚ü∂ X} {g : W ‚ü∂ Y} {h : X ‚ü∂ Z} {i : Y ‚ü∂ Z}
  (p : comm_sq f g h i) : comm_sq i.op h.op g.op f.op :=
‚ü®by simp only [‚Üê op_comp, p.w]‚ü©

@[simps]
def unop {W X Y Z : C·µí·µñ} {f : W ‚ü∂ X} {g : W ‚ü∂ Y} {h : X ‚ü∂ Z} {i : Y ‚ü∂ Z}
  (p : comm_sq f g h i) : comm_sq i.unop h.unop g.unop f.unop :=
‚ü®by simp only [‚Üê unop_comp, p.w]‚ü©

@[simps]
def cone {W X Y Z : C} {f : W ‚ü∂ X} {g : W ‚ü∂ Y} {h : X ‚ü∂ Z} {i : Y ‚ü∂ Z}
  (p : comm_sq f g h i) : pullback_cone h i := pullback_cone.mk _ _ p.w

@[simps]
def cocone {W X Y Z : C} {f : W ‚ü∂ X} {g : W ‚ü∂ Y} {h : X ‚ü∂ Z} {i : Y ‚ü∂ Z}
  (p : comm_sq f g h i) : pushout_cocone f g := pushout_cocone.mk _ _ p.w

end comm_sq

/-- The proposition that a square
```
  P --fst--> X
  |          |
 snd         f
  |          |
  v          v
  Y ---g---> Z

```
is a pullback square.
-/
structure is_pullback
  {P X Y Z : C} (fst : P ‚ü∂ X) (snd : P ‚ü∂ Y) (f : X ‚ü∂ Z) (g : Y ‚ü∂ Z)
  extends comm_sq fst snd f g : Prop :=
(is_limit' : nonempty (is_limit (pullback_cone.mk _ _ w)))

/-- The proposition that a square
```
  Z ---f---> X
  |          |
  g         inl
  |          |
  v          v
  Y --inr--> P

```
is a pushout square.
-/
structure is_pushout
  {Z X Y P : C} (f : Z ‚ü∂ X) (g : Z ‚ü∂ Y) (inl : X ‚ü∂ P) (inr : Y ‚ü∂ P)
  extends comm_sq f g inl inr : Prop :=
(is_colimit' : nonempty (is_colimit (pushout_cocone.mk _ _ w)))

/-!
We begin by providing some glue between `is_pullback` and the `is_limit` and `has_limit` APIs.
(And similarly for `is_pushout`.)
-/

namespace is_pullback

variables {P X Y Z : C} {fst : P ‚ü∂ X} {snd : P ‚ü∂ Y} {f : X ‚ü∂ Z} {g : Y ‚ü∂ Z}

/--
The `pullback_cone f g` implicit in the statement that we have a `is_pullback fst snd f g`.
-/
abbreviation cone (h : is_pullback fst snd f g) : pullback_cone f g := h.to_comm_sq.cone

/--
The cone obtained from `is_pullback fst snd f g` is a limit cone.
-/
noncomputable def is_limit (h : is_pullback fst snd f g) : is_limit h.cone :=
h.is_limit'.some

/-- If `c` is a limiting pullback cone, then we have a `is_pullback c.fst c.snd f g`. -/
lemma of_is_limit {c : pullback_cone f g} (h : limits.is_limit c) :
  is_pullback c.fst c.snd f g :=
{ w := c.condition,
  is_limit' := ‚ü®is_limit.of_iso_limit h
    (limits.pullback_cone.ext (iso.refl _) (by tidy) (by tidy))‚ü©, }

lemma of_is_limit' (p : comm_sq fst snd f g) (h : limits.is_limit p.cone) :
  is_pullback fst snd f g :=
{ w := p.w,
  is_limit' := ‚ü®is_limit.of_iso_limit h
    (limits.pullback_cone.ext (iso.refl _) (by tidy) (by tidy))‚ü©, }

/-- The pullback provided by `has_pullback f g` fits into a `is_pullback`. -/
lemma of_has_pullback (f : X ‚ü∂ Z) (g : Y ‚ü∂ Z) [has_pullback f g] :
  is_pullback (pullback.fst : pullback f g ‚ü∂ X) (pullback.snd : pullback f g ‚ü∂ Y) f g :=
of_is_limit (limit.is_limit (cospan f g))

end is_pullback

namespace is_pushout

variables {Z X Y P : C} {f : Z ‚ü∂ X} {g : Z ‚ü∂ Y} {inl : X ‚ü∂ P} {inr : Y ‚ü∂ P}

/--
The `pushout_cocone f g` implicit in the statement that we have a `is_pushout f g inl inr`.
-/
abbreviation cocone (h : is_pushout f g inl inr) : pushout_cocone f g := h.to_comm_sq.cocone

/--
The cocone obtained from `is_pushout f g inl inr` is a colimit cocone.
-/
noncomputable def is_colimit (h : is_pushout f g inl inr) : is_colimit h.cocone :=
h.is_colimit'.some

/-- If `c` is a colimiting pushout cocone, then we have a `is_pushout f g c.inl c.inr`. -/
lemma of_is_colimit {c : pushout_cocone f g} (h : limits.is_colimit c) :
  is_pushout f g c.inl c.inr :=
{ w := c.condition,
  is_colimit' := ‚ü®is_colimit.of_iso_colimit h
    (limits.pushout_cocone.ext (iso.refl _) (by tidy) (by tidy))‚ü©, }

lemma of_is_colimit' (p : comm_sq f g inl inr) (h : limits.is_colimit p.cocone) :
  is_pushout f g inl inr :=
{ w := p.w,
  is_colimit' := ‚ü®is_colimit.of_iso_colimit h
    (limits.pushout_cocone.ext (iso.refl _) (by tidy) (by tidy))‚ü©, }

/-- The pushout provided by `has_pushout f g` fits into a `is_pushout`. -/
lemma of_has_pushout (f : Z ‚ü∂ X) (g : Z ‚ü∂ Y) [has_pushout f g] :
  is_pushout f g (pushout.inl : X ‚ü∂ pushout f g) (pushout.inr : Y ‚ü∂ pushout f g) :=
of_is_colimit (colimit.is_colimit (span f g))

variables (X Y)

lemma of_has_binary_coproduct [has_binary_coproduct X Y] [has_initial C] :
  is_pushout (initial.to X) (initial.to Y) coprod.inl coprod.inr :=
{ w := by apply subsingleton.elim,
  is_colimit' := nonempty.intro (pushout_cocone.is_colimit_aux _ (Œª s, coprod.desc (s.Œπ.app walking_span.left) (s.Œπ.app walking_span.right)) ((Œª s, coprod.inl_desc _ _)) (Œª s, coprod.inr_desc _ _)
    begin
      intros s m h,
      ext,
      { simpa only [coprod.inl_desc] using h walking_span.left, },
      { simpa only [coprod.inr_desc] using h walking_span.right, },
    end) }

end is_pushout

namespace is_pullback

variables {P X Y Z : C} {fst : P ‚ü∂ X} {snd : P ‚ü∂ Y} {f : X ‚ü∂ Z} {g : Y ‚ü∂ Z}

lemma flip (h : is_pullback fst snd f g) : is_pullback snd fst g f :=
of_is_limit (@pullback_cone.flip_is_limit _ _ _ _ _ _ _ _ _ _ h.w.symm h.is_limit)

section

variables [has_zero_object C] [has_zero_morphisms C]
open_locale zero_object

/-- The square with `0 : 0 ‚ü∂ 0` on the left and `ùüô X` on the right is a pullback square. -/
lemma zero_left (X : C) : is_pullback (0 : 0 ‚ü∂ X) (0 : 0 ‚ü∂ 0) (ùüô X) (0 : 0 ‚ü∂ X) :=
{ w := by simp,
  is_limit' :=
  ‚ü®{ lift := Œª s, 0,
     fac' := Œª s, by simpa using @pullback_cone.equalizer_ext _ _ _ _ _ _ _ s _ 0 (ùüô _)
       (by simpa using (pullback_cone.condition s).symm), }‚ü© }

/-- The square with `0 : 0 ‚ü∂ 0` on the top and `ùüô X` on the bottom is a pullback square. -/
lemma zero_top (X : C) : is_pullback (0 : 0 ‚ü∂ 0) (0 : 0 ‚ü∂ X) (0 : 0 ‚ü∂ X) (ùüô X) :=
(zero_left X).flip

end

/-- Paste two pullback squares "vertically" to obtain another pullback square. -/
-- Objects here are arranged in a 3x2 grid, and indexed by their xy coordinates.
-- Morphisms are named `h·µ¢‚±º` for a horizontal morphism starting at `(i,j)`,
-- and `v·µ¢‚±º` for a vertical morphism starting at `(i,j)`.
lemma paste_vert {X‚ÇÅ‚ÇÅ X‚ÇÅ‚ÇÇ X‚ÇÇ‚ÇÅ X‚ÇÇ‚ÇÇ X‚ÇÉ‚ÇÅ X‚ÇÉ‚ÇÇ : C}
  {h‚ÇÅ‚ÇÅ : X‚ÇÅ‚ÇÅ ‚ü∂ X‚ÇÅ‚ÇÇ} {h‚ÇÇ‚ÇÅ : X‚ÇÇ‚ÇÅ ‚ü∂ X‚ÇÇ‚ÇÇ} {h‚ÇÉ‚ÇÅ : X‚ÇÉ‚ÇÅ ‚ü∂ X‚ÇÉ‚ÇÇ}
  {v‚ÇÅ‚ÇÅ : X‚ÇÅ‚ÇÅ ‚ü∂ X‚ÇÇ‚ÇÅ} {v‚ÇÅ‚ÇÇ : X‚ÇÅ‚ÇÇ ‚ü∂ X‚ÇÇ‚ÇÇ} {v‚ÇÇ‚ÇÅ : X‚ÇÇ‚ÇÅ ‚ü∂ X‚ÇÉ‚ÇÅ} {v‚ÇÇ‚ÇÇ : X‚ÇÇ‚ÇÇ ‚ü∂ X‚ÇÉ‚ÇÇ}
  (s : is_pullback h‚ÇÅ‚ÇÅ v‚ÇÅ‚ÇÅ v‚ÇÅ‚ÇÇ h‚ÇÇ‚ÇÅ) (t : is_pullback h‚ÇÇ‚ÇÅ v‚ÇÇ‚ÇÅ v‚ÇÇ‚ÇÇ h‚ÇÉ‚ÇÅ) :
  is_pullback h‚ÇÅ‚ÇÅ (v‚ÇÅ‚ÇÅ ‚â´ v‚ÇÇ‚ÇÅ) (v‚ÇÅ‚ÇÇ ‚â´ v‚ÇÇ‚ÇÇ) h‚ÇÉ‚ÇÅ :=
(of_is_limit
  (big_square_is_pullback _ _ _ _ _ _ _ s.w t.w t.is_limit s.is_limit))

/-- Paste two pullback squares "horizontally" to obtain another pullback square. -/
lemma paste_horiz {X‚ÇÅ‚ÇÅ X‚ÇÅ‚ÇÇ X‚ÇÅ‚ÇÉ X‚ÇÇ‚ÇÅ X‚ÇÇ‚ÇÇ X‚ÇÇ‚ÇÉ : C}
  {h‚ÇÅ‚ÇÅ : X‚ÇÅ‚ÇÅ ‚ü∂ X‚ÇÅ‚ÇÇ} {h‚ÇÅ‚ÇÇ : X‚ÇÅ‚ÇÇ ‚ü∂ X‚ÇÅ‚ÇÉ} {h‚ÇÇ‚ÇÅ : X‚ÇÇ‚ÇÅ ‚ü∂ X‚ÇÇ‚ÇÇ} {h‚ÇÇ‚ÇÇ : X‚ÇÇ‚ÇÇ ‚ü∂ X‚ÇÇ‚ÇÉ}
  {v‚ÇÅ‚ÇÅ : X‚ÇÅ‚ÇÅ ‚ü∂ X‚ÇÇ‚ÇÅ} {v‚ÇÅ‚ÇÇ : X‚ÇÅ‚ÇÇ ‚ü∂ X‚ÇÇ‚ÇÇ} {v‚ÇÅ‚ÇÉ : X‚ÇÅ‚ÇÉ ‚ü∂ X‚ÇÇ‚ÇÉ}
  (s : is_pullback h‚ÇÅ‚ÇÅ v‚ÇÅ‚ÇÅ v‚ÇÅ‚ÇÇ h‚ÇÇ‚ÇÅ) (t : is_pullback h‚ÇÅ‚ÇÇ v‚ÇÅ‚ÇÇ v‚ÇÅ‚ÇÉ h‚ÇÇ‚ÇÇ) :
  is_pullback (h‚ÇÅ‚ÇÅ ‚â´ h‚ÇÅ‚ÇÇ) v‚ÇÅ‚ÇÅ v‚ÇÅ‚ÇÉ (h‚ÇÇ‚ÇÅ ‚â´ h‚ÇÇ‚ÇÇ) :=
(paste_vert s.flip t.flip).flip

/-- Given a pullback square assembled from a commuting square on the top and
a pullback square on the bottom, the top square is a pullback square. -/
lemma of_bot {X‚ÇÅ‚ÇÅ X‚ÇÅ‚ÇÇ X‚ÇÇ‚ÇÅ X‚ÇÇ‚ÇÇ X‚ÇÉ‚ÇÅ X‚ÇÉ‚ÇÇ : C}
  {h‚ÇÅ‚ÇÅ : X‚ÇÅ‚ÇÅ ‚ü∂ X‚ÇÅ‚ÇÇ} {h‚ÇÇ‚ÇÅ : X‚ÇÇ‚ÇÅ ‚ü∂ X‚ÇÇ‚ÇÇ} {h‚ÇÉ‚ÇÅ : X‚ÇÉ‚ÇÅ ‚ü∂ X‚ÇÉ‚ÇÇ}
  {v‚ÇÅ‚ÇÅ : X‚ÇÅ‚ÇÅ ‚ü∂ X‚ÇÇ‚ÇÅ} {v‚ÇÅ‚ÇÇ : X‚ÇÅ‚ÇÇ ‚ü∂ X‚ÇÇ‚ÇÇ} {v‚ÇÇ‚ÇÅ : X‚ÇÇ‚ÇÅ ‚ü∂ X‚ÇÉ‚ÇÅ} {v‚ÇÇ‚ÇÇ : X‚ÇÇ‚ÇÇ ‚ü∂ X‚ÇÉ‚ÇÇ}
  (s : is_pullback h‚ÇÅ‚ÇÅ (v‚ÇÅ‚ÇÅ ‚â´ v‚ÇÇ‚ÇÅ) (v‚ÇÅ‚ÇÇ ‚â´ v‚ÇÇ‚ÇÇ) h‚ÇÉ‚ÇÅ) (p : h‚ÇÅ‚ÇÅ ‚â´ v‚ÇÅ‚ÇÇ = v‚ÇÅ‚ÇÅ ‚â´ h‚ÇÇ‚ÇÅ)
  (t : is_pullback h‚ÇÇ‚ÇÅ v‚ÇÇ‚ÇÅ v‚ÇÇ‚ÇÇ h‚ÇÉ‚ÇÅ) :
  is_pullback h‚ÇÅ‚ÇÅ v‚ÇÅ‚ÇÅ v‚ÇÅ‚ÇÇ h‚ÇÇ‚ÇÅ :=
of_is_limit (left_square_is_pullback _ _ _ _ _ _ _ p _ t.is_limit s.is_limit)

/-- Given a pullback square assembled from a commuting square on the left and
a pullback square on the right, the left square is a pullback square. -/
lemma of_right {X‚ÇÅ‚ÇÅ X‚ÇÅ‚ÇÇ X‚ÇÅ‚ÇÉ X‚ÇÇ‚ÇÅ X‚ÇÇ‚ÇÇ X‚ÇÇ‚ÇÉ : C}
  {h‚ÇÅ‚ÇÅ : X‚ÇÅ‚ÇÅ ‚ü∂ X‚ÇÅ‚ÇÇ} {h‚ÇÅ‚ÇÇ : X‚ÇÅ‚ÇÇ ‚ü∂ X‚ÇÅ‚ÇÉ} {h‚ÇÇ‚ÇÅ : X‚ÇÇ‚ÇÅ ‚ü∂ X‚ÇÇ‚ÇÇ} {h‚ÇÇ‚ÇÇ : X‚ÇÇ‚ÇÇ ‚ü∂ X‚ÇÇ‚ÇÉ}
  {v‚ÇÅ‚ÇÅ : X‚ÇÅ‚ÇÅ ‚ü∂ X‚ÇÇ‚ÇÅ} {v‚ÇÅ‚ÇÇ : X‚ÇÅ‚ÇÇ ‚ü∂ X‚ÇÇ‚ÇÇ} {v‚ÇÅ‚ÇÉ : X‚ÇÅ‚ÇÉ ‚ü∂ X‚ÇÇ‚ÇÉ}
  (s : is_pullback (h‚ÇÅ‚ÇÅ ‚â´ h‚ÇÅ‚ÇÇ) v‚ÇÅ‚ÇÅ v‚ÇÅ‚ÇÉ (h‚ÇÇ‚ÇÅ ‚â´ h‚ÇÇ‚ÇÇ)) (p : h‚ÇÅ‚ÇÅ ‚â´ v‚ÇÅ‚ÇÇ = v‚ÇÅ‚ÇÅ ‚â´ h‚ÇÇ‚ÇÅ)
  (t : is_pullback h‚ÇÅ‚ÇÇ v‚ÇÅ‚ÇÇ v‚ÇÅ‚ÇÉ h‚ÇÇ‚ÇÇ) :
  is_pullback h‚ÇÅ‚ÇÅ v‚ÇÅ‚ÇÅ v‚ÇÅ‚ÇÇ h‚ÇÇ‚ÇÅ :=
(of_bot s.flip p.symm t.flip).flip

end is_pullback

namespace is_pushout

variables {Z X Y P : C} {f : Z ‚ü∂ X} {g : Z ‚ü∂ Y} {inl : X ‚ü∂ P} {inr : Y ‚ü∂ P}

lemma flip (h : is_pushout f g inl inr) : is_pushout g f inr inl :=
of_is_colimit (@pushout_cocone.flip_is_colimit _ _ _ _ _ _ _ _ _ _ h.w.symm h.is_colimit)

section

variables [has_zero_object C] [has_zero_morphisms C]
open_locale zero_object

/-- The square with `0 : 0 ‚ü∂ 0` on the right and `ùüô X` on the left is a pushout square. -/
lemma zero_right (X : C) : is_pushout (0 : X ‚ü∂ 0) (ùüô X) (0 : 0 ‚ü∂ 0) (0 : X ‚ü∂ 0) :=
{ w := by simp,
  is_colimit' :=
  ‚ü®{ desc := Œª s, 0,
     fac' := Œª s, begin
       have c := @pushout_cocone.coequalizer_ext _ _ _ _ _ _ _ s _ 0 (ùüô _) (by simp)
         (by simpa using (pushout_cocone.condition s)),
      dsimp at c,
      simpa using c,
     end }‚ü© }

/-- The square with `0 : 0 ‚ü∂ 0` on the bottom and `ùüô X` on the top is a pushout square. -/
lemma zero_bot (X : C) : is_pushout (ùüô X) (0 : X ‚ü∂ 0) (0 : X ‚ü∂ 0) (0 : 0 ‚ü∂ 0) :=
(zero_right X).flip

end

/-- Paste two pushout squares "vertically" to obtain another pushout square. -/
-- Objects here are arranged in a 3x2 grid, and indexed by their xy coordinates.
-- Morphisms are named `h·µ¢‚±º` for a horizontal morphism starting at `(i,j)`,
-- and `v·µ¢‚±º` for a vertical morphism starting at `(i,j)`.
lemma paste_vert {X‚ÇÅ‚ÇÅ X‚ÇÅ‚ÇÇ X‚ÇÇ‚ÇÅ X‚ÇÇ‚ÇÇ X‚ÇÉ‚ÇÅ X‚ÇÉ‚ÇÇ : C}
  {h‚ÇÅ‚ÇÅ : X‚ÇÅ‚ÇÅ ‚ü∂ X‚ÇÅ‚ÇÇ} {h‚ÇÇ‚ÇÅ : X‚ÇÇ‚ÇÅ ‚ü∂ X‚ÇÇ‚ÇÇ} {h‚ÇÉ‚ÇÅ : X‚ÇÉ‚ÇÅ ‚ü∂ X‚ÇÉ‚ÇÇ}
  {v‚ÇÅ‚ÇÅ : X‚ÇÅ‚ÇÅ ‚ü∂ X‚ÇÇ‚ÇÅ} {v‚ÇÅ‚ÇÇ : X‚ÇÅ‚ÇÇ ‚ü∂ X‚ÇÇ‚ÇÇ} {v‚ÇÇ‚ÇÅ : X‚ÇÇ‚ÇÅ ‚ü∂ X‚ÇÉ‚ÇÅ} {v‚ÇÇ‚ÇÇ : X‚ÇÇ‚ÇÇ ‚ü∂ X‚ÇÉ‚ÇÇ}
  (s : is_pushout h‚ÇÅ‚ÇÅ v‚ÇÅ‚ÇÅ v‚ÇÅ‚ÇÇ h‚ÇÇ‚ÇÅ) (t : is_pushout h‚ÇÇ‚ÇÅ v‚ÇÇ‚ÇÅ v‚ÇÇ‚ÇÇ h‚ÇÉ‚ÇÅ) :
  is_pushout h‚ÇÅ‚ÇÅ (v‚ÇÅ‚ÇÅ ‚â´ v‚ÇÇ‚ÇÅ) (v‚ÇÅ‚ÇÇ ‚â´ v‚ÇÇ‚ÇÇ) h‚ÇÉ‚ÇÅ :=
(of_is_colimit
  (big_square_is_pushout _ _ _ _ _ _ _ s.w t.w t.is_colimit s.is_colimit))

/-- Paste two pushout squares "horizontally" to obtain another pushout square. -/
lemma paste_horiz {X‚ÇÅ‚ÇÅ X‚ÇÅ‚ÇÇ X‚ÇÅ‚ÇÉ X‚ÇÇ‚ÇÅ X‚ÇÇ‚ÇÇ X‚ÇÇ‚ÇÉ : C}
  {h‚ÇÅ‚ÇÅ : X‚ÇÅ‚ÇÅ ‚ü∂ X‚ÇÅ‚ÇÇ} {h‚ÇÅ‚ÇÇ : X‚ÇÅ‚ÇÇ ‚ü∂ X‚ÇÅ‚ÇÉ} {h‚ÇÇ‚ÇÅ : X‚ÇÇ‚ÇÅ ‚ü∂ X‚ÇÇ‚ÇÇ} {h‚ÇÇ‚ÇÇ : X‚ÇÇ‚ÇÇ ‚ü∂ X‚ÇÇ‚ÇÉ}
  {v‚ÇÅ‚ÇÅ : X‚ÇÅ‚ÇÅ ‚ü∂ X‚ÇÇ‚ÇÅ} {v‚ÇÅ‚ÇÇ : X‚ÇÅ‚ÇÇ ‚ü∂ X‚ÇÇ‚ÇÇ} {v‚ÇÅ‚ÇÉ : X‚ÇÅ‚ÇÉ ‚ü∂ X‚ÇÇ‚ÇÉ}
  (s : is_pushout h‚ÇÅ‚ÇÅ v‚ÇÅ‚ÇÅ v‚ÇÅ‚ÇÇ h‚ÇÇ‚ÇÅ) (t : is_pushout h‚ÇÅ‚ÇÇ v‚ÇÅ‚ÇÇ v‚ÇÅ‚ÇÉ h‚ÇÇ‚ÇÇ) :
  is_pushout (h‚ÇÅ‚ÇÅ ‚â´ h‚ÇÅ‚ÇÇ) v‚ÇÅ‚ÇÅ v‚ÇÅ‚ÇÉ (h‚ÇÇ‚ÇÅ ‚â´ h‚ÇÇ‚ÇÇ) :=
(paste_vert s.flip t.flip).flip

/-- Given a pushout square assembled from a pushout square on the top and
a commuting square on the bottom, the bottom square is a pushout square. -/
lemma of_bot {X‚ÇÅ‚ÇÅ X‚ÇÅ‚ÇÇ X‚ÇÇ‚ÇÅ X‚ÇÇ‚ÇÇ X‚ÇÉ‚ÇÅ X‚ÇÉ‚ÇÇ : C}
  {h‚ÇÅ‚ÇÅ : X‚ÇÅ‚ÇÅ ‚ü∂ X‚ÇÅ‚ÇÇ} {h‚ÇÇ‚ÇÅ : X‚ÇÇ‚ÇÅ ‚ü∂ X‚ÇÇ‚ÇÇ} {h‚ÇÉ‚ÇÅ : X‚ÇÉ‚ÇÅ ‚ü∂ X‚ÇÉ‚ÇÇ}
  {v‚ÇÅ‚ÇÅ : X‚ÇÅ‚ÇÅ ‚ü∂ X‚ÇÇ‚ÇÅ} {v‚ÇÅ‚ÇÇ : X‚ÇÅ‚ÇÇ ‚ü∂ X‚ÇÇ‚ÇÇ} {v‚ÇÇ‚ÇÅ : X‚ÇÇ‚ÇÅ ‚ü∂ X‚ÇÉ‚ÇÅ} {v‚ÇÇ‚ÇÇ : X‚ÇÇ‚ÇÇ ‚ü∂ X‚ÇÉ‚ÇÇ}
  (s : is_pushout h‚ÇÅ‚ÇÅ (v‚ÇÅ‚ÇÅ ‚â´ v‚ÇÇ‚ÇÅ) (v‚ÇÅ‚ÇÇ ‚â´ v‚ÇÇ‚ÇÇ) h‚ÇÉ‚ÇÅ) (p : h‚ÇÇ‚ÇÅ ‚â´ v‚ÇÇ‚ÇÇ = v‚ÇÇ‚ÇÅ ‚â´ h‚ÇÉ‚ÇÅ)
  (t : is_pushout h‚ÇÅ‚ÇÅ v‚ÇÅ‚ÇÅ v‚ÇÅ‚ÇÇ h‚ÇÇ‚ÇÅ) :
  is_pushout h‚ÇÇ‚ÇÅ v‚ÇÇ‚ÇÅ v‚ÇÇ‚ÇÇ h‚ÇÉ‚ÇÅ :=
of_is_colimit (right_square_is_pushout _ _ _ _ _ _ _ _ p t.is_colimit s.is_colimit)

/-- Given a pushout square assembled from a pushout square on the left and
a commuting square on the right, the right square is a pushout square. -/
lemma of_right {X‚ÇÅ‚ÇÅ X‚ÇÅ‚ÇÇ X‚ÇÅ‚ÇÉ X‚ÇÇ‚ÇÅ X‚ÇÇ‚ÇÇ X‚ÇÇ‚ÇÉ : C}
  {h‚ÇÅ‚ÇÅ : X‚ÇÅ‚ÇÅ ‚ü∂ X‚ÇÅ‚ÇÇ} {h‚ÇÅ‚ÇÇ : X‚ÇÅ‚ÇÇ ‚ü∂ X‚ÇÅ‚ÇÉ} {h‚ÇÇ‚ÇÅ : X‚ÇÇ‚ÇÅ ‚ü∂ X‚ÇÇ‚ÇÇ} {h‚ÇÇ‚ÇÇ : X‚ÇÇ‚ÇÇ ‚ü∂ X‚ÇÇ‚ÇÉ}
  {v‚ÇÅ‚ÇÅ : X‚ÇÅ‚ÇÅ ‚ü∂ X‚ÇÇ‚ÇÅ} {v‚ÇÅ‚ÇÇ : X‚ÇÅ‚ÇÇ ‚ü∂ X‚ÇÇ‚ÇÇ} {v‚ÇÅ‚ÇÉ : X‚ÇÅ‚ÇÉ ‚ü∂ X‚ÇÇ‚ÇÉ}
  (s : is_pushout (h‚ÇÅ‚ÇÅ ‚â´ h‚ÇÅ‚ÇÇ) v‚ÇÅ‚ÇÅ v‚ÇÅ‚ÇÉ (h‚ÇÇ‚ÇÅ ‚â´ h‚ÇÇ‚ÇÇ)) (p : h‚ÇÅ‚ÇÇ ‚â´ v‚ÇÅ‚ÇÉ = v‚ÇÅ‚ÇÇ ‚â´ h‚ÇÇ‚ÇÇ)
  (t : is_pushout h‚ÇÅ‚ÇÅ v‚ÇÅ‚ÇÅ v‚ÇÅ‚ÇÇ h‚ÇÇ‚ÇÅ) :
  is_pushout h‚ÇÅ‚ÇÇ v‚ÇÅ‚ÇÇ v‚ÇÅ‚ÇÉ h‚ÇÇ‚ÇÇ :=
(of_bot s.flip p.symm t.flip).flip

end is_pushout

namespace is_pullback

def op_cone {W X Y Z : C} {f : W ‚ü∂ X} {g : W ‚ü∂ Y} {h : X ‚ü∂ Z} {i : Y ‚ü∂ Z}
  (p : comm_sq f g h i) : p.cone.op ‚âÖ p.flip.op.cocone :=
cocones.ext (iso.refl _) (by { rintro (_|_|_), tidy, })

lemma op {Z X Y P : C} (f : Z ‚ü∂ X) (g : Z ‚ü∂ Y) (inl : X ‚ü∂ P) (inr : Y ‚ü∂ P)
  (h : is_pullback f g inl inr) : is_pushout inr.op inl.op g.op f.op :=
begin
  apply is_pushout.of_is_colimit' h.to_comm_sq.op,
  apply is_colimit.of_iso_colimit (pullback_cone.op_is_colimit _ h.flip.is_limit'.some),
  apply op_cone,
end

@[simp]
lemma unop_id {X : C·µí·µñ} : (ùüô X).unop = ùüô X.unop := rfl

@[simp]
lemma id_app {D : Type*} [category D] (F : C ‚•§ D) (X : C) : nat_trans.app (ùüô F) X = ùüô _ := rfl

def unop_cone {W X Y Z : C·µí·µñ} {f : W ‚ü∂ X} {g : W ‚ü∂ Y} {h : X ‚ü∂ Z} {i : Y ‚ü∂ Z}
  (p : comm_sq f g h i) : p.cone.unop ‚âÖ p.flip.unop.cocone := cocones.ext (iso.refl _)
(by { rintro (_|_|_); { dsimp, simp, repeat { erw category.id_comp, }, refl, }, })

lemma unop {Z X Y P : C·µí·µñ} (f : Z ‚ü∂ X) (g : Z ‚ü∂ Y) (inl : X ‚ü∂ P) (inr : Y ‚ü∂ P)
  (h : is_pullback f g inl inr) : is_pushout inr.unop inl.unop g.unop f.unop :=
begin
  apply is_pushout.of_is_colimit' h.to_comm_sq.unop,
  apply is_colimit.of_iso_colimit (pullback_cone.unop_is_colimit _ h.flip.is_limit'.some),
  apply unop_cone,
end

end is_pullback

namespace is_pushout

def op_cocone {Z X Y P : C} (f : Z ‚ü∂ X) (g : Z ‚ü∂ Y) (inl : X ‚ü∂ P) (inr : Y ‚ü∂ P)
  (p : comm_sq f g inl inr) : p.cocone.op ‚âÖ p.flip.op.cone :=
cones.ext (iso.refl _) (by { rintro (_|_|_); { dsimp, simp, erw category.comp_id, refl, }, })

lemma op {Z X Y P : C} (f : Z ‚ü∂ X) (g : Z ‚ü∂ Y) (inl : X ‚ü∂ P) (inr : Y ‚ü∂ P)
  (h : is_pushout f g inl inr) : is_pullback inr.op inl.op g.op f.op :=
begin
  apply is_pullback.of_is_limit' h.to_comm_sq.op,
  apply is_limit.of_iso_limit (pushout_cocone.op_is_limit _ h.flip.is_colimit'.some),
  apply op_cocone,
end

def unop_cocone {Z X Y P : C·µí·µñ} (f : Z ‚ü∂ X) (g : Z ‚ü∂ Y) (inl : X ‚ü∂ P) (inr : Y ‚ü∂ P)
  (p : comm_sq f g inl inr) : p.cocone.unop ‚âÖ p.flip.unop.cone := cones.ext (iso.refl _)
(by { rintro (_|_|_); { dsimp, simp, repeat { erw category.comp_id, }, refl, }, })

lemma unop {Z X Y P : C·µí·µñ} (f : Z ‚ü∂ X) (g : Z ‚ü∂ Y) (inl : X ‚ü∂ P) (inr : Y ‚ü∂ P)
  (h : is_pushout f g inl inr) : is_pullback inr.unop inl.unop g.unop f.unop :=
begin
  apply is_pullback.of_is_limit' h.to_comm_sq.unop,
  apply is_limit.of_iso_limit (pushout_cocone.unop_is_limit _ h.flip.is_colimit'.some),
  apply unop_cocone,
end

end is_pushout

end category_theory.limits
