import for_mathlib.algebra.homology.trunc

noncomputable theory

open category_theory category_theory.category category_theory.limits
open_locale zero_object

namespace category_theory

namespace triangulated

open pretriangulated

variables {C : Type*} [category C] [has_shift C ‚Ñ§] [preadditive C] [has_zero_object C]
  [‚àÄ (n : ‚Ñ§), functor.additive (shift_functor C n)] [pretriangulated C]

variable (S : set C)

class is_triangulated_subcategory : Prop :=
(zero : (0 : C) ‚àà S)
(shift : ‚àÄ (X : C) (n : ‚Ñ§) (hX : X ‚àà S), (shift_functor C n).obj X ‚àà S)
(ext‚ÇÇ : ‚àÄ (T : triangle C) (hT : T ‚àà dist_triang C) (h‚ÇÅ : T.obj‚ÇÅ ‚àà S)
  (h‚ÇÉ : T.obj‚ÇÉ ‚àà S), T.obj‚ÇÇ ‚àà S)

class is_triangulated_subcategory' : Prop :=
(zero : ‚àÉ (X : C) (hX‚ÇÄ : is_zero X), X ‚àà S)
(shift : ‚àÄ (X : C) (n : ‚Ñ§) (hX : X ‚àà S), (shift_functor C n).obj X ‚àà S)
(distinguished_cocone_triangle' : ‚àÄ (X Y : C) (hX : X ‚àà S) (hY : Y ‚àà S) (f : X ‚ü∂ Y),
  ‚àÉ (Z : C) (hZ : Z ‚àà S) (g : Y ‚ü∂ Z) (h : Z ‚ü∂ X‚ü¶(1 : ‚Ñ§)‚üß),
    triangle.mk f g h ‚àà dist_triang C)

@[priority 100]
instance is_triangulated_subcategory'_of_is_triangulated_subcategory
  [hS : is_triangulated_subcategory S] :
  is_triangulated_subcategory' S :=
{ zero := ‚ü®0, is_zero_zero _, hS.zero‚ü©,
  shift := hS.shift,
  distinguished_cocone_triangle' := Œª X Y hX hY f, begin
    obtain ‚ü®Z, g, h, mem‚ü© := pretriangulated.distinguished_cocone_triangle _ _ f,
    exact ‚ü®Z, hS.ext‚ÇÇ _ (rot_of_dist_triangle _ _ mem) hY (hS.shift _ 1 hX), g, h, mem‚ü©,
  end, }

namespace is_triangulated_subcategory'

variable [hS : is_triangulated_subcategory' S]

include hS

instance : has_zero_object (full_subcategory S) :=
begin
  obtain ‚ü®X‚ÇÄ, hX‚ÇÄ, mem‚ü© := hS.zero,
  refine ‚ü®‚ü®‚ü®X‚ÇÄ, mem‚ü©, _‚ü©‚ü©,
  rw limits.is_zero.iff_id_eq_zero,
  exact (full_subcategory_inclusion S).map_injective (is_zero.eq_of_src hX‚ÇÄ _ _),
end

instance is_stable_by_shift : S.is_stable_by_shift ‚Ñ§ :=
‚ü®Œª a X hX, hS.shift X a hX‚ü©

instance shift_functor_additive (n : ‚Ñ§) :
  (category_theory.shift_functor (full_subcategory S) n).additive :=
‚ü®begin
  rintros ‚ü®K, hK‚ü© ‚ü®L, hL‚ü© f g,
  exact (full_subcategory_inclusion S).map_injective (category_theory.shift_functor C n).map_add,
end‚ü©

--instance full_subcategory_inclusion_has_comm_shift :
--  (full_subcategory_inclusion S).has_comm_shift ‚Ñ§ := infer_instance

def distinguished_triangles : set (triangle (full_subcategory S)) :=
Œª T, (full_subcategory_inclusion S).map_triangle.obj T ‚àà dist_triang C

lemma isomorphic_distinguished (T‚ÇÅ : triangle (full_subcategory S))
  (hT‚ÇÅ : T‚ÇÅ ‚àà distinguished_triangles S) (T‚ÇÇ : triangle (full_subcategory S)) (e : T‚ÇÇ ‚âÖ T‚ÇÅ) :
  T‚ÇÇ ‚àà distinguished_triangles S :=
pretriangulated.isomorphic_distinguished _ hT‚ÇÅ _ ((full_subcategory_inclusion S).map_triangle.map_iso e)

lemma contractible_distinguished (X : full_subcategory S) :
  triangle.mk (ùüô X) (0 : X ‚ü∂ 0) 0 ‚àà distinguished_triangles S :=
begin
  refine pretriangulated.isomorphic_distinguished _
    (pretriangulated.contractible_distinguished ((full_subcategory_inclusion S).obj X)) _ _,
  refine triangle.mk_iso _ _ (iso.refl _) (iso.refl _)
    (full_subcategory_inclusion S).map_zero_object _ _ _,
  tidy,
end

lemma rotate_distinguished_triangle (T : triangle (full_subcategory S)) :
  T ‚àà distinguished_triangles S ‚Üî
    T.rotate ‚àà distinguished_triangles S :=
begin
  change ((full_subcategory_inclusion S).map_triangle.obj T ‚àà dist_triang C) ‚Üî
    ((full_subcategory_inclusion S).map_triangle.obj T.rotate ‚àà dist_triang C),
  rw pretriangulated.rotate_distinguished_triangle,
  let e := (full_subcategory_inclusion S).map_triangle_rotate.app T,
  split,
  { exact Œª h, pretriangulated.isomorphic_distinguished _ h _ e.symm, },
  { exact Œª h, pretriangulated.isomorphic_distinguished _ h _ e, },
end

lemma distinguished_cocone_triangle (X Y : full_subcategory S) (f : X ‚ü∂ Y) :
  ‚àÉ (Z : full_subcategory S) (g : Y ‚ü∂ Z) (h : Z ‚ü∂ (category_theory.shift_functor _ (1 : ‚Ñ§)).obj X),
    triangle.mk f g h ‚àà distinguished_triangles S :=
begin
  obtain ‚ü®X, hX‚ü© := X,
  obtain ‚ü®Y, hY‚ü© := Y,
  obtain ‚ü®Z, hZ, g, h, mem‚ü© := hS.distinguished_cocone_triangle' _ _ hX hY f,
  refine ‚ü®‚ü®Z, hZ‚ü©, g, h, pretriangulated.isomorphic_distinguished _ mem _ _‚ü©,
  refine triangle.mk_iso _ _ (iso.refl _) (iso.refl _) (iso.refl _) (by tidy) (by tidy) _,
  { dsimp,
    simp only [functor.map_id, comp_id, id_comp],
    apply comp_id, },
end

lemma complete_distinguished_triangle_morphism (T‚ÇÅ T‚ÇÇ : triangle (full_subcategory S))
  (hT‚ÇÅ : T‚ÇÅ ‚àà distinguished_triangles S) (hT‚ÇÇ : T‚ÇÇ ‚àà distinguished_triangles S)
  (a : T‚ÇÅ.obj‚ÇÅ ‚ü∂ T‚ÇÇ.obj‚ÇÅ) (b : T‚ÇÅ.obj‚ÇÇ ‚ü∂ T‚ÇÇ.obj‚ÇÇ) (h : T‚ÇÅ.mor‚ÇÅ ‚â´ b = a ‚â´ T‚ÇÇ.mor‚ÇÅ) :
  ‚àÉ (c : T‚ÇÅ.obj‚ÇÉ ‚ü∂ T‚ÇÇ.obj‚ÇÉ), T‚ÇÅ.mor‚ÇÇ ‚â´ c = b ‚â´ T‚ÇÇ.mor‚ÇÇ ‚àß T‚ÇÅ.mor‚ÇÉ ‚â´
    (category_theory.shift_functor _ 1).map a = c ‚â´ T‚ÇÇ.mor‚ÇÉ :=
begin
  obtain ‚ü®c, ‚ü®hc‚ÇÅ, hc‚ÇÇ‚ü©‚ü© := pretriangulated.complete_distinguished_triangle_morphism
    ((full_subcategory_inclusion S).map_triangle.obj T‚ÇÅ)
      ((full_subcategory_inclusion S).map_triangle.obj T‚ÇÇ)
    hT‚ÇÅ hT‚ÇÇ a b h,
  refine ‚ü®c, ‚ü®hc‚ÇÅ, _‚ü©‚ü©,
  dsimp at hc‚ÇÇ,
  erw [comp_id, comp_id] at hc‚ÇÇ,
  exact hc‚ÇÇ,
end

instance : pretriangulated (full_subcategory S) :=
{ distinguished_triangles := distinguished_triangles S,
  isomorphic_distinguished := isomorphic_distinguished S,
  contractible_distinguished := contractible_distinguished S,
  distinguished_cocone_triangle := distinguished_cocone_triangle S,
  rotate_distinguished_triangle := rotate_distinguished_triangle S,
  complete_distinguished_triangle_morphism := complete_distinguished_triangle_morphism S, }

instance [is_triangulated C] : is_triangulated (full_subcategory S) :=
‚ü®Œª X‚ÇÅ X‚ÇÇ X‚ÇÉ Z‚ÇÅ‚ÇÇ Z‚ÇÇ‚ÇÉ Z‚ÇÅ‚ÇÉ u‚ÇÅ‚ÇÇ u‚ÇÇ‚ÇÉ u‚ÇÅ‚ÇÉ comm v‚ÇÅ‚ÇÇ w‚ÇÅ‚ÇÇ h‚ÇÅ‚ÇÇ v‚ÇÇ‚ÇÉ w‚ÇÇ‚ÇÉ h‚ÇÇ‚ÇÉ v‚ÇÅ‚ÇÉ w‚ÇÅ‚ÇÉ h‚ÇÅ‚ÇÉ, begin
  have comm' := (full_subcategory_inclusion S).congr_map comm,
  rw [functor.map_comp] at comm',
  have H := (is_triangulated.octahedron_axiom comm' h‚ÇÅ‚ÇÇ h‚ÇÇ‚ÇÉ h‚ÇÅ‚ÇÉ).some,
  obtain ‚ü®m‚ÇÅ, m‚ÇÉ, comm‚ÇÅ, comm‚ÇÇ, comm‚ÇÉ, comm‚ÇÑ, H'‚ü© := H,
  refine nonempty.intro
  { m‚ÇÅ := m‚ÇÅ,
    m‚ÇÉ := m‚ÇÉ,
    comm‚ÇÅ := comm‚ÇÅ,
    comm‚ÇÇ := begin
      erw [comp_id, comp_id] at comm‚ÇÇ,
      exact comm‚ÇÇ,
    end,
    comm‚ÇÉ := comm‚ÇÉ,
    comm‚ÇÑ := begin
      erw [comp_id, comp_id] at comm‚ÇÑ,
      exact comm‚ÇÑ,
    end,
    mem := begin
      change _ ‚àà dist_triang C,
      refine pretriangulated.isomorphic_distinguished _ H' _ _,
      refine triangle.mk_iso _ _ (iso.refl _) (iso.refl _) (iso.refl _) _ _ _,
      { dsimp,
        simp, },
      { dsimp,
        simp, },
      { dsimp,
        erw [functor.map_id, comp_id, comp_id, id_comp, comp_id],
        refl, },
    end, }
end‚ü©

end is_triangulated_subcategory'

end triangulated

end category_theory

variables {C : Type*} [category C] [abelian C]

namespace derived_category

instance zero_is_le (n : ‚Ñ§) : (0 : derived_category C).is_le n :=
‚ü®Œª i hi, is_zero.of_iso (is_zero_zero _)
  (derived_category.homology_functor C i).map_zero_object‚ü©

instance zero_is_ge (n : ‚Ñ§) : (0 : derived_category C).is_ge n :=
‚ü®Œª i hi, is_zero.of_iso (is_zero_zero _)
  (derived_category.homology_functor C i).map_zero_object‚ü©

def is_plus (K : derived_category C) : Prop := ‚àÉ (n : ‚Ñ§), K.is_ge n

variable (C)
open category_theory.triangulated

instance plus_is_triangulated_subcategory :
  is_triangulated_subcategory (Œª (K : derived_category C), K.is_plus) :=
{ zero := ‚ü®0, infer_instance‚ü©,
  shift := begin
    rintros K k ‚ü®n, hn‚ü©,
    haveI := hn,
    exact ‚ü®n-k, shift_is_ge K n k (n-k) (by linarith)‚ü©,
  end,
  ext‚ÇÇ := begin
    rintros T hT ‚ü®n‚ÇÅ, hn‚ÇÅ‚ü© ‚ü®n‚ÇÉ, hn‚ÇÉ‚ü©,
    exact ‚ü®min n‚ÇÅ n‚ÇÉ,
      ‚ü®Œª n hn, short_complex.exact.is_zero_of_both_zeros (homology_sequence.ex‚ÇÇ hT n)
        (is_zero.eq_of_src (hn‚ÇÅ.is_zero' _ (lt_of_lt_of_le hn (min_le_left n‚ÇÅ n‚ÇÉ))) _ _)
        (is_zero.eq_of_tgt (hn‚ÇÉ.is_zero' _ (lt_of_lt_of_le hn (min_le_right n‚ÇÅ n‚ÇÉ))) _ _)‚ü©‚ü©,
  end, }

abbreviation plus := full_subcategory (Œª (K : derived_category C), K.is_plus)

namespace plus

instance : pretriangulated (plus C) := infer_instance

def mk (K : derived_category C) (n : ‚Ñ§) [hn : K.is_ge n] :
  derived_category.plus C :=
‚ü®K, n, hn‚ü©

variable {C}

def Œπ : plus C ‚•§ derived_category C :=
full_subcategory_inclusion _

end plus

end derived_category

namespace cochain_complex

def is_plus (K : cochain_complex C ‚Ñ§) : Prop :=
  ‚àÉ (n : ‚Ñ§), K.is_strictly_ge n

instance zero_is_strictly_ge (n : ‚Ñ§) : is_strictly_ge (0 : cochain_complex C ‚Ñ§) n :=
‚ü®Œª k hk, is_zero.of_iso (is_zero_zero _)
  (homological_complex.eval C (complex_shape.up ‚Ñ§) k).map_zero_object‚ü©

instance zero_is_strictly_le (n : ‚Ñ§) : is_strictly_le (0 : cochain_complex C ‚Ñ§) n :=
‚ü®Œª k hk, is_zero.of_iso (is_zero_zero _)
  (homological_complex.eval C (complex_shape.up ‚Ñ§) k).map_zero_object‚ü©

lemma mapping_cone_is_strictly_le {K L : cochain_complex C ‚Ñ§} (f : K ‚ü∂ L) (n k l : ‚Ñ§)
  [K.is_strictly_le k] [L.is_strictly_le l] (hk : k ‚â§ n+1) (hl : l ‚â§ n) :
  (mapping_cone f).is_strictly_le n :=
‚ü®Œª i hi, begin
  simp only [mapping_cone.X_is_zero_iff],
  split,
  { exact is_strictly_le.is_zero K k (i+1) (by linarith), },
  { exact is_strictly_le.is_zero L l i (by linarith), },
end‚ü©

lemma mapping_cone_is_strictly_ge {K L : cochain_complex C ‚Ñ§} (f : K ‚ü∂ L) (n k l : ‚Ñ§)
  [K.is_strictly_ge k] [L.is_strictly_ge l] (hk : n+1 ‚â§ k) (hl : n ‚â§ l) :
  (mapping_cone f).is_strictly_ge n :=
‚ü®Œª i hi, begin
  simp only [mapping_cone.X_is_zero_iff],
  split,
  { exact is_strictly_ge.is_zero K k (i+1) (by linarith), },
  { exact is_strictly_ge.is_zero L l i (by linarith), },
end‚ü©

lemma mapping_cone_is_plus {K L : cochain_complex C ‚Ñ§} (f : K ‚ü∂ L)
  (hK : K.is_plus) (hL : L.is_plus) : (mapping_cone f).is_plus :=
begin
  obtain ‚ü®k, hK‚ü© := hK,
  obtain ‚ü®l, hL‚ü© := hL,
  haveI := hK,
  haveI := hL,
  exact ‚ü®min (k-1) l, mapping_cone_is_strictly_ge f _ k l
    (by linarith [min_le_left (k-1) l]) (min_le_right _ _)‚ü©,
end

variable (C)
abbreviation plus :=
full_subcategory (Œª (K : cochain_complex C ‚Ñ§), cochain_complex.is_plus K)

namespace plus

variable {C}

abbreviation Œπ : plus C ‚•§ cochain_complex C ‚Ñ§ :=
full_subcategory_inclusion _

variable (C)

def shift_functor (k : ‚Ñ§) : plus C ‚•§ plus C :=
full_subcategory.lift _ (Œπ ‚ãô shift_functor _ k) (Œª K, begin
  obtain ‚ü®n, hn‚ü© := K.2,
  haveI := hn,
  refine ‚ü®n-k, _‚ü©,
  dsimp,
  exact shift_is_strict_ge K.1 n k (n-k) (by linarith),
end)

instance : has_shift (plus C) ‚Ñ§ :=
has_shift_of_fully_faithful Œπ (shift_functor C)
  (Œª n, full_subcategory.lift_comp_inclusion _ _ _)

end plus

end cochain_complex

open category_theory.triangulated

namespace homotopy_category

variable (C)

def is_plus : set (homotopy_category C (complex_shape.up ‚Ñ§)) :=
Œª K, cochain_complex.is_plus K.1

abbreviation plus :=
full_subcategory (homotopy_category.is_plus C)

instance plus_is_triangulated_subcategory' :
  category_theory.triangulated.is_triangulated_subcategory' (is_plus C) :=
{ zero := begin
    refine ‚ü®‚ü®0‚ü©, _, ‚ü®0, infer_instance‚ü©‚ü©,
    rw is_zero.iff_id_eq_zero,
    change (homotopy_category.quotient _ _).map (ùüô 0) = 0,
    simp only [id_zero, functor.map_zero],
  end,
  shift := begin
    rintro ‚ü®X‚ü© n hX,
    exact ((cochain_complex.plus.shift_functor C n).obj ‚ü®X, hX‚ü©).2,
  end,
  distinguished_cocone_triangle' := begin
    rintro ‚ü®X‚ü© ‚ü®Y‚ü© hX hY ‚ü®f : X ‚ü∂ Y‚ü©,
    refine ‚ü®_, _, _, _, ‚ü®_, _, f, ‚ü®iso.refl _‚ü©‚ü©‚ü©,
    exact cochain_complex.mapping_cone_is_plus f hX hY,
  end, }

namespace plus

variable {C}

abbreviation Œπ : plus C ‚•§ homotopy_category C (complex_shape.up ‚Ñ§) :=
full_subcategory_inclusion _

variable (C)

def homology_functor (n : ‚Ñ§) : plus C ‚•§ C :=
Œπ ‚ãô homotopy_category.homology_functor C (complex_shape.up ‚Ñ§) n

def quasi_isomorphisms : morphism_property (plus C) :=
  Œª K L œÜ, ‚àÄ (n : ‚Ñ§), is_iso ((homology_functor C n).map œÜ)

end plus

end homotopy_category

namespace derived_category

namespace plus

def Qh : homotopy_category.plus C ‚•§ derived_category.plus C :=
full_subcategory.lift _ (homotopy_category.plus.Œπ ‚ãô Qh)
begin
  rintro ‚ü®‚ü®K‚ü©, n, hn‚ü©,
  refine ‚ü®n, (_ : (Q.obj K).is_ge n)‚ü©,
  rw ‚Üê cochain_complex.is_ge_iff_Q_obj_is_ge,
  dsimp at hn,
  haveI := hn,
  exact cochain_complex.is_ge_of_is_strictly_ge K n,
end

end plus

end derived_category
