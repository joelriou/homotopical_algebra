import for_mathlib.algebra.homology.k_projective
import category_theory.abelian.injective

noncomputable theory

open category_theory category_theory.category category_theory.limits

namespace category_theory

namespace short_complex

variables {C : Type*} [category C] [has_zero_morphisms C]
  (S : short_complex C) [has_homology S]

def homology_op_iso : S.op.homology ‚âÖ opposite.op S.homology :=
begin
  let h := S.some_homology_data,
  exact h.op.homology_iso ‚â™‚â´ h.iso.op ‚â™‚â´ h.homology_iso.op,
end

end short_complex

end category_theory

open category_theory

namespace cochain_complex

variables {C : Type*} [category C] [abelian C]

namespace op_equivalence

@[simps]
def op_obj (K : cochain_complex C ‚Ñ§) :
  cochain_complex C·µí·µñ ‚Ñ§ :=
{ X := Œª n, opposite.op (K.X (-n)),
  d := Œª n m, (K.d _ _).op,
  shape' := Œª i j hij, begin
    rw K.shape,
    { refl, },
    { simp only [complex_shape.up_rel] at ‚ä¢ hij,
      intro h,
      apply hij,
      linarith, },
  end,
  d_comp_d' := Œª i j k hij hjk,
    by simpa only [‚Üê op_comp, homological_complex.d_comp_d], }

@[simps]
def unop_obj (K : cochain_complex C·µí·µñ ‚Ñ§) :
  cochain_complex C ‚Ñ§ :=
{ X := Œª n, opposite.unop (K.X (-n)),
  d := Œª n m, (K.d _ _).unop,
  shape' := Œª i j hij, begin
    rw K.shape,
    { refl, },
    { simp only [complex_shape.up_rel] at ‚ä¢ hij,
      intro h,
      apply hij,
      linarith, },
  end,
  d_comp_d' := Œª i j k hij hjk,
    by simpa only [‚Üê unop_comp, homological_complex.d_comp_d], }

@[simps]
def unop_op_obj (K : cochain_complex C ‚Ñ§) :
  unop_obj (op_obj K) ‚âÖ K :=
homological_complex.hom.iso_of_components
  (Œª n, homological_complex.X_iso_of_eq K (by linarith)) (by tidy)

@[simps]
def op_unop_obj (K : cochain_complex C·µí·µñ ‚Ñ§) :
  op_obj (unop_obj K) ‚âÖ K :=
homological_complex.hom.iso_of_components
  (Œª n, homological_complex.X_iso_of_eq K (by linarith)) (by tidy)

variable (C)

@[simps]
def functor : (cochain_complex C ‚Ñ§)·µí·µñ ‚•§ cochain_complex C·µí·µñ ‚Ñ§ :=
{ obj := Œª K, op_obj (opposite.unop K) ,
  map := Œª K L œÜ,
  { f := Œª n, (œÜ.unop.f (-n)).op,
    comm' := Œª i j hij, begin
      dsimp,
      simp only [‚Üê op_comp, homological_complex.hom.comm],
    end, }, }

@[simps]
def inverse : cochain_complex C·µí·µñ ‚Ñ§ ‚•§ (cochain_complex C ‚Ñ§)·µí·µñ :=
{ obj := Œª K, opposite.op (unop_obj K),
  map := Œª K L œÜ, quiver.hom.op
  { f := Œª n, (œÜ.f (-n)).unop,
    comm' := Œª i j hij, begin
      dsimp,
      simp only [‚Üê unop_comp, homological_complex.hom.comm],
    end, }, }

@[simps]
def unit_iso :
  ùü≠ (cochain_complex C ‚Ñ§)·µí·µñ ‚âÖ op_equivalence.functor C ‚ãô op_equivalence.inverse C :=
nat_iso.of_components (Œª K, (unop_op_obj (opposite.unop K)).op)
  (Œª K L f, quiver.hom.unop_inj begin
    ext n,
    dsimp,
    symmetry,
    apply homological_complex.X_iso_of_eq_hom_naturality f.unop,
  end)

@[simps]
def counit_iso :
  op_equivalence.inverse C ‚ãô op_equivalence.functor C ‚âÖ ùü≠ (cochain_complex C·µí·µñ ‚Ñ§) :=
nat_iso.of_components (Œª K, (op_unop_obj K))
  (Œª K L f, begin
    ext n,
    dsimp,
    apply homological_complex.X_iso_of_eq_hom_naturality f,
  end)

end op_equivalence

variable (C)

@[simps]
def op_equivalence : (cochain_complex C ‚Ñ§)·µí·µñ ‚âå cochain_complex C·µí·µñ ‚Ñ§ :=
{ functor := op_equivalence.functor C,
  inverse := op_equivalence.inverse C,
  unit_iso := op_equivalence.unit_iso C,
  counit_iso := op_equivalence.counit_iso C,
  functor_unit_iso_comp' := Œª K, begin
    ext n,
    dsimp [homological_complex.X_iso_of_eq],
    simp only [eq_to_hom_op, eq_to_hom_trans, eq_to_hom_refl],
  end, }

variable {C}

lemma op_obj_is_strictly_le (K : cochain_complex C ‚Ñ§) (n : ‚Ñ§) [K.is_strictly_ge (-n)] :
  (op_equivalence.op_obj K).is_strictly_le n :=
‚ü®Œª i hi, (cochain_complex.is_strictly_ge.is_zero K (-n) (-i) (by linarith)).op‚ü©

lemma op_obj_is_strictly_ge (K : cochain_complex C ‚Ñ§) (n : ‚Ñ§) [K.is_strictly_le (-n)] :
  (op_equivalence.op_obj K).is_strictly_ge n :=
‚ü®Œª i hi, (cochain_complex.is_strictly_le.is_zero K (-n) (-i) (by linarith)).op‚ü©

lemma unop_obj_is_strictly_le (K : cochain_complex C·µí·µñ ‚Ñ§) (n : ‚Ñ§) [K.is_strictly_ge (-n)] :
  (op_equivalence.unop_obj K).is_strictly_le n :=
‚ü®Œª i hi, (cochain_complex.is_strictly_ge.is_zero K (-n) (-i) (by linarith)).unop‚ü©

lemma unop_obj_is_strictly_ge (K : cochain_complex C·µí·µñ ‚Ñ§) (n : ‚Ñ§) [K.is_strictly_le (-n)] :
  (op_equivalence.unop_obj K).is_strictly_ge n :=
‚ü®Œª i hi, (cochain_complex.is_strictly_le.is_zero K (-n) (-i) (by linarith)).unop‚ü©

def unop_homotopy {K L : cochain_complex C ‚Ñ§} {f‚ÇÅ f‚ÇÇ : K ‚ü∂ L}
  (h : homotopy ((op_equivalence.functor C).map f‚ÇÅ.op) ((op_equivalence.functor C).map f‚ÇÇ.op)) :
  homotopy f‚ÇÅ f‚ÇÇ :=
{ hom := Œª i j, (K.X_iso_of_eq (by simp)).hom ‚â´ (h.hom (-j) (-i)).unop ‚â´
      (L.X_iso_of_eq (by simp)).hom,
  zero' := Œª i j hij, begin
    rw [h.zero, unop_zero, zero_comp, comp_zero],
    simp only [complex_shape.up_rel] at hij ‚ä¢,
    intro h,
    apply hij,
    linarith,
  end,
  comm := Œª n, quiver.hom.op_inj begin
    obtain ‚ü®m, rfl‚ü© : ‚àÉ (m : ‚Ñ§), n = -m := ‚ü®-n, by rw neg_neg n‚ü©,
    have eq := h.comm m,
    have eq‚ÇÅ : (complex_shape.up ‚Ñ§).rel m (m+1) := rfl,
    have eq‚ÇÇ : (complex_shape.up ‚Ñ§).rel (m-1) m := by simp,
    have eq‚ÇÉ : (complex_shape.up ‚Ñ§).rel (-m) (-(m-1)),
    { simp only [neg_sub, complex_shape.up_rel], linarith, },
    have eq‚ÇÑ : (complex_shape.up ‚Ñ§).rel (-(m+1)) (-m) := by simp,
    rw [d_next_eq _ eq‚ÇÅ, prev_d_eq _ eq‚ÇÇ] at eq,
    rw [d_next_eq _ eq‚ÇÉ, prev_d_eq _ eq‚ÇÑ],
    dsimp at eq ‚ä¢,
    rw eq,
    have eq‚ÇÖ : ‚àÄ (a a' b b' : ‚Ñ§) (ha : a = a') (hb : b = b'),
      h.hom a b = eq_to_hom (by rw ha) ‚â´ h.hom a' b' ‚â´ eq_to_hom (by rw hb),
    { intros a a' b b' ha hb,
      substs ha hb,
      simp only [eq_to_hom_refl, id_comp, comp_id], },
    conv_lhs { congr, rw add_comm, },
    congr' 3;
    { dsimp [homological_complex.X_iso_of_eq],
      simp only [assoc, eq_to_hom_op],
      exact eq‚ÇÖ _ _ _ _ (neg_neg _).symm (neg_neg _).symm, },
  end, }

def homology_op_iso (K : cochain_complex C ‚Ñ§) (n : ‚Ñ§) :
  (op_equivalence.op_obj K).homology n ‚âÖ opposite.op (K.homology (-n)) :=
begin
  have r‚ÇÅ : (complex_shape.up ‚Ñ§).rel (-(n+1)) (-n) := by simp,
  have r‚ÇÇ : (complex_shape.up ‚Ñ§).rel (-n) (-(n-1)) := by { rw complex_shape.up_rel, linarith, },
  have r‚ÇÉ : (complex_shape.up ‚Ñ§).rel (n-1) n := by simp,
  have r‚ÇÑ : (complex_shape.up ‚Ñ§).rel n (n+1) := by simp,
  refine _ ‚â™‚â´ short_complex.homology_op_iso _ ‚â™‚â´
    (short_complex.homology_map_iso
      ((homological_complex.short_complex_functor_nat_iso C _ r‚ÇÅ r‚ÇÇ).app K)).op,
  exact short_complex.homology_map_iso
    ((homological_complex.short_complex_functor_nat_iso C·µí·µñ _ r‚ÇÉ r‚ÇÑ).app ((op_equivalence.op_obj K))),
end

lemma acyclic_op {K : cochain_complex C ‚Ñ§} (hK : homological_complex.acyclic K) :
  homological_complex.acyclic (op_equivalence.op_obj K) :=
Œª n, is_zero.of_iso (hK (-n)).op (homology_op_iso K n)

end cochain_complex
