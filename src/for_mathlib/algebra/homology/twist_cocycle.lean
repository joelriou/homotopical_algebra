/-
Copyright (c) 2022 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/

import for_mathlib.algebra.homology.hom_complex

noncomputable theory

open category_theory category_theory.category category_theory.limits category_theory.preadditive

namespace cochain_complex

namespace hom_complex

variables {C : Type*} [category C] [preadditive C]

variables {F G K : cochain_complex C ‚Ñ§} {n : ‚Ñ§} (z : cocycle F G n) {m : ‚Ñ§} [‚àÄ p, has_binary_biproduct (F.X (p+1-n)) (G.X p)]
include z

namespace twist

@[protected, simp]
def Œ¥ (p q : ‚Ñ§) :
  biprod (F.X (p+1-n)) (G.X p) ‚ü∂ biprod (F.X (q+1-n)) (G.X q) :=
begin
  refine biprod.desc (biprod.lift (Œµ (n+1) ‚Ä¢ F.d (p+1-n) (q+1-n)) _) (biprod.lift 0 (G.d p q)),
  by_cases p+1 = q,
  { exact (z : cochain F G n).v (p+1-n) q (show q=(p+1-n)+n, by linarith), },
  { exact 0, },
end

end twist

@[simps]
def twist : cochain_complex C ‚Ñ§ :=
{ X := Œª p, biprod (F.X (p+1-n)) (G.X p),
  d := Œª p q, twist.Œ¥ z p q,
  shape' := Œª p q hpq, begin
    dsimp [twist.Œ¥],
    ext,
    { simp only [biprod.inl_desc, biprod.lift_fst, comp_zero, zero_comp],
      rw [F.shape, smul_zero],
      intro h,
      apply hpq,
      change p+1-n+1=q+1-n at h,
      change p+1=q,
      linarith, },
    { simp only [biprod.inl_desc, biprod.lift_snd, comp_zero, zero_comp],
      split_ifs,
      { exfalso, exact hpq h, },
      { refl, }, },
    { simp only [biprod.inr_desc, biprod.lift_fst, comp_zero, zero_comp], },
    { simp only [biprod.inr_desc, biprod.lift_snd, comp_zero, zero_comp, G.shape p q hpq], },
  end,
  d_comp_d' := Œª i j k hij hjk, begin
    change i+1=j at hij,
    subst hij,
    change i+1+1=k at hjk,
    have hjk' : i+2 = k := by linarith,
    subst hjk',
    simp only [twist.Œ¥, dif_pos rfl, dif_pos (show i+1+1 = i+2, by linarith)],
    ext,
    { simp only [add_zero, biprod.inl_desc_assoc, biprod.lift_desc, linear.smul_comp,
        add_comp, assoc, biprod.lift_fst,
        linear.comp_smul, homological_complex.d_comp_d, smul_zero, comp_zero, zero_comp], },
    { simp only [biprod.inl_desc_assoc, biprod.lift_desc, linear.smul_comp,
        add_comp, assoc, biprod.lift_snd, comp_zero, zero_comp],
      have hz‚ÇÅ := z.2,
      rw cocycle.mem_iff n (n+1) rfl at hz‚ÇÅ,
      have hz‚ÇÇ := cochain.congr_v hz‚ÇÅ (i+1-n) (i+2) (by linarith),
      simp only [Œ¥_v n (n+1) rfl _ (i+1-n) (i+2) (by linarith) (i+1) (i+1+1-n)
        (by linarith) (by linarith), cochain.zero_v] at hz‚ÇÇ,
      rw ‚Üê hz‚ÇÇ,
      abel, },
    { simp only [zero_add, biprod.inr_desc_assoc, biprod.lift_desc, zero_comp, assoc, biprod.lift_fst, comp_zero], },
    { simp only [zero_add, biprod.inr_desc_assoc, biprod.lift_desc, zero_comp, assoc, biprod.lift_snd, homological_complex.d_comp_d,
  comp_zero], },
  end }

namespace twist

omit z
lemma biprod_is_zero_iff {C : Type*} [category C] [preadditive C] (A B : C) [has_binary_biproduct A B] :
  is_zero (A ‚äû B) ‚Üî is_zero A ‚àß is_zero B :=
begin
  simp only [is_zero.iff_id_eq_zero],
  split,
  { intro h,
    split,
    { suffices : ùüô A = biprod.inl ‚â´ (ùüô (A ‚äû B)) ‚â´ biprod.fst,
      { rw [this, h, zero_comp, comp_zero], },
      rw [id_comp, biprod.inl_fst], },
    { suffices : ùüô B = biprod.inr ‚â´ (ùüô (A ‚äû B)) ‚â´ biprod.snd,
      { rw [this, h, zero_comp, comp_zero], },
      rw [id_comp, biprod.inr_snd], }, },
  { intro h,
    ext,
    { simpa only [comp_id, biprod.inl_fst, comp_zero, zero_comp] using h.left, },
    { simp only [comp_id, biprod.inl_snd, comp_zero, zero_comp], },
    { simp only [comp_id, biprod.inr_fst, comp_zero, zero_comp], },
    { simpa only [comp_id, biprod.inr_snd, comp_zero, zero_comp] using h.right, } },
end

lemma is_bounded_above (z : cocycle F G n) (hF : F.is_bounded_above) (hG : G.is_bounded_above) :
  (twist z).is_bounded_above :=
begin
  cases hF with r hr,
  cases hG with s hs,
  use max (r+n-1) s,
  intros i hi,
  dsimp only [twist],
  rw biprod_is_zero_iff,
  split,
  { apply hr,
    have h := lt_of_le_of_lt (le_max_left _ _) hi,
    linarith, },
  { apply hs,
    exact lt_of_le_of_lt (le_max_right _ _) hi, },
end

include z
def inl {n‚ÇÄ : ‚Ñ§} (hn‚ÇÄ : n‚ÇÄ+1=n) : cochain F (twist z) n‚ÇÄ :=
cochain.mk (Œª p q hpq, (cochain.of_hom (ùüô F)).v p (q+1-n) (by linarith) ‚â´ biprod.inl)

def inr : G ‚ü∂ twist z := { f := Œª p, biprod.inr, }

def fst {n‚ÇÅ : ‚Ñ§} (hn‚ÇÅ : n+n‚ÇÅ=1) : cocycle (twist z) F n‚ÇÅ :=
cocycle.mk (cochain.mk (Œª p q hpq, biprod.fst ‚â´
  (cochain.of_hom (ùüô F)).v (p+1-n) q (show q=p+1-n+0, by linarith))) (n‚ÇÅ+1) rfl
begin
  have hn‚ÇÅ' : n‚ÇÅ = 1-n := by linarith,
  subst hn‚ÇÅ',
  ext1,
  simp only [Œ¥_v (1-n) (1-n+1) rfl _ p q hpq (p+1-n) (p+1) (by linarith) rfl,
    cochain.mk_v, cochain.of_hom_v, homological_complex.id_f, comp_id, twist_d, twist.Œ¥, dif_pos,
      cochain.zero_v],
  ext,
  { have hq : q = p+1+1-n := by linarith,
    subst hq,
    have eq : Œµ (1-n+1) * Œµ (n+1) = Œµ ((1 : ‚Ñ§) + 1 + 1) := by { rw ‚Üê Œµ_add, congr' 1, linarith, },
    have eq' : Œµ ((1 : ‚Ñ§)+1+1) = -1 := by { simp only [Œµ_succ, Œµ_1, neg_neg], },
    simp only [biprod.inl_fst_assoc, biprod.inl_desc_assoc, biprod.lift_fst, comp_add,
      cochain.of_hom_v, homological_complex.id_f, comp_zsmul, comp_id, comp_zero, smul_smul,
      eq, eq', neg_smul, one_zsmul, add_right_neg], },
  { simp only [zero_add, neg_eq_zero, comp_add, biprod.inr_fst_assoc, zero_comp,
      linear.comp_smul, biprod.inr_desc_assoc, biprod.lift_fst_assoc, smul_zero, comp_zero], },
end

def snd : cochain (twist z) G 0 :=
cochain.mk (Œª p q hpq, biprod.snd ‚â´ (cochain.of_hom (ùüô G)).v p q hpq)

@[simp]
lemma inl_comp_fst {n‚ÇÄ n‚ÇÅ : ‚Ñ§} (hn‚ÇÄ : n‚ÇÄ+1=n) (hn‚ÇÅ : n+n‚ÇÅ=1) :
  (inl z hn‚ÇÄ).comp ‚Üë(fst z hn‚ÇÅ) (show 0=n‚ÇÄ+n‚ÇÅ, by linarith) = cochain.of_hom (ùüô F) :=
begin
  ext,
  dsimp [cochain.comp, cochain.mk, cochain.v, cochain.of_hom, cochain.of_homs, inl, fst],
  simp only [id_comp, assoc, biprod.inl_fst_assoc, eq_to_hom_trans, eq_to_hom_refl],
end

@[simp]
lemma inl_comp_snd {n‚ÇÄ : ‚Ñ§} (hn‚ÇÄ : n‚ÇÄ+1=n) :
  (inl z hn‚ÇÄ).comp (snd z) (add_zero n‚ÇÄ).symm = 0 :=
begin
  ext,
  simp only [inl, snd, cochain.comp, cochain.mk, cochain.v, cochain.of_hom, cochain.of_homs,
    assoc, biprod.inl_snd_assoc, zero_comp, comp_zero, cochain.zero_v],
end

@[simp]
lemma inr_comp_fst {n‚ÇÅ : ‚Ñ§} (hn‚ÇÅ : n+n‚ÇÅ=1) :
  (cochain.of_hom (inr z)).comp (fst z hn‚ÇÅ : cochain (twist z) F n‚ÇÅ) (zero_add n‚ÇÅ).symm = 0 :=
begin
  ext,
  simp only [inr, fst, cochain.zero_cochain_comp, cochain.of_hom_v, cocycle.mk_coe,
    cochain.mk_v, biprod.inr_fst_assoc, zero_comp, cochain.zero_v],
end

@[simp]
lemma inr_comp_snd :
  (cochain.of_hom (inr z)).comp (snd z) (add_zero 0).symm = cochain.of_hom (ùüô G) :=
begin
  ext,
  simp only [inr, snd, cochain.comp_zero_cochain, cochain.mk_v, cochain.of_hom_v,
    homological_complex.id_f, comp_id, biprod.inr_snd],
end

@[simp]
lemma Œ¥_inl {n‚ÇÄ : ‚Ñ§} (hn‚ÇÄ : n‚ÇÄ+1=n) :
  Œ¥ n‚ÇÄ n (inl z hn‚ÇÄ) = cochain.comp ‚Üëz (cochain.of_hom (inr z)) (add_zero n).symm :=
begin
  ext1,
  simp only [Œ¥_v n‚ÇÄ n hn‚ÇÄ (inl z hn‚ÇÄ) p q hpq _ _ rfl rfl, twist_d, twist.Œ¥],
  ext,
  { simp only [‚Üê hn‚ÇÄ, inl, cochain.mk_v, Œµ_succ, neg_neg, assoc, biprod.inl_desc, neg_smul,
      add_comp, biprod.lift_fst, comp_zsmul, cochain.of_hom_v_comp_d,
      homological_complex.id_f, id_comp, neg_comp, zsmul_comp, biprod.inl_fst, comp_id,
      cochain.d_comp_of_hom_v, add_right_neg, cochain.comp_zero_cochain, cochain.of_hom_v,
      inr, biprod.inr_fst, comp_zero], },
  { simp only [inl, inr, add_zero, sub_add_cancel, eq_self_iff_true, cochain.mk_v,
      dif_pos, assoc, biprod.inl_desc, add_comp, biprod.lift_snd,
      linear.smul_comp, biprod.inl_snd, comp_zero, smul_zero, cochain.comp_zero_cochain,
      cochain.of_hom_v, biprod.inr_snd, comp_id, id_comp,
      cochain.zero_cochain_comp' _ _ p (q-1+1-n) q, homological_complex.id_f], },
end

@[simp]
lemma Œ¥_snd {n‚ÇÅ : ‚Ñ§} (hn‚ÇÅ : n+n‚ÇÅ=1) :
  Œ¥ 0 1 (snd z) = -cochain.comp (fst z hn‚ÇÅ : cochain (twist z) F n‚ÇÅ) (‚Üëz) (show 1 = n‚ÇÅ+n, by rw [‚Üê hn‚ÇÅ, add_comm]) :=
begin
  ext1,
  simp only [Œ¥_v 0 1 (zero_add 1) _ p q hpq p q (by linarith) hpq, fst, snd, zero_add, Œµ_1,
    cochain.mk_v, cochain.of_hom_v, homological_complex.id_f, comp_id, neg_zsmul, one_zsmul,
    cochain.neg_v, cocycle.mk_coe, twist_d, twist.Œ¥,
    cochain.comp_v _ _ (show 1=n‚ÇÅ+n, by linarith) p (p+1-n) q (by linarith) (by linarith)],
  ext,
  { simp only [dif_pos hpq.symm, zero_add, comp_add, biprod.inl_snd_assoc, zero_comp,
      comp_neg, biprod.inl_desc_assoc, biprod.lift_snd, biprod.inl_fst_assoc], },
  { simp only [neg_zero, comp_add, biprod.inr_snd_assoc, comp_neg, biprod.inr_desc_assoc,
      biprod.lift_snd, add_right_neg, biprod.inr_fst_assoc, zero_comp], },
end

lemma id_eq {n‚ÇÄ n‚ÇÅ : ‚Ñ§} (hn‚ÇÄ : n‚ÇÄ+1=n)  (hn‚ÇÅ : n+n‚ÇÅ=1) : cochain.of_hom (ùüô (twist z)) =
cochain.comp ‚Üë(fst z hn‚ÇÅ) (inl z hn‚ÇÄ) (show 0=n‚ÇÅ+n‚ÇÄ, by linarith) +
cochain.comp (snd z) (cochain.of_hom (inr z)) (zero_add 0).symm :=
begin
  ext1,
  simpa only [fst, inl, snd, inr, cochain.add_v,
    cochain.comp_v _ _ (show 0 = n‚ÇÅ+n‚ÇÄ, by linarith) p (p+1-n) p (by linarith) (by linarith),
    cochain.of_hom_v, homological_complex.id_f, cocycle.mk_coe, cochain.mk_v,
    comp_id, id_comp, cochain.comp_zero_cochain, biprod.total],
end

lemma cochain_ext (y‚ÇÅ y‚ÇÇ : cochain (twist z) K m) {n‚ÇÄ n‚ÇÅ : ‚Ñ§} (hn‚ÇÄ : n‚ÇÄ+1=n)
  (hn‚ÇÅ : n‚ÇÅ = n‚ÇÄ+m) :
  y‚ÇÅ = y‚ÇÇ ‚Üî cochain.comp (inl z hn‚ÇÄ) y‚ÇÅ hn‚ÇÅ = cochain.comp (inl z hn‚ÇÄ) y‚ÇÇ hn‚ÇÅ ‚àß
    cochain.comp (cochain.of_hom (inr z)) y‚ÇÅ (zero_add m).symm =
      cochain.comp (cochain.of_hom (inr z)) y‚ÇÇ (zero_add m).symm :=
begin
  split,
  { intro h, rw h, tauto, },
  { rintro ‚ü®hl, hr‚ü©,
    suffices : cochain.comp (cochain.of_hom (ùüô _)) y‚ÇÅ (zero_add m).symm =
      cochain.comp (cochain.of_hom (ùüô _)) y‚ÇÇ (zero_add m).symm,
    { ext1,
      simpa only [cochain.id_comp] using cochain.congr_v this p q hpq, },
    simp only [id_eq z hn‚ÇÄ (show n+(-n‚ÇÄ)=1, by linarith), cochain.add_comp,
      cochain.comp_assoc_of_second_is_zero_cochain,
      cochain.comp_assoc _ _ _ (show 0=-n‚ÇÄ+n‚ÇÄ, by linarith) (show n‚ÇÅ=n‚ÇÄ+m, by linarith)
      (show m=-n‚ÇÄ+n‚ÇÄ+m, by linarith), hl, hr], }
end

def desc_cochain {m m‚ÇÅ : ‚Ñ§} (y‚ÇÅ : cochain F K m‚ÇÅ) (y‚ÇÇ : cochain G K m)
  (hm‚ÇÅ : m‚ÇÅ+1=n+m) : cochain (twist z) K m :=
cochain.comp ‚Üë(fst z (show n+(m-m‚ÇÅ) = 1, by linarith)) y‚ÇÅ (eq_add_of_sub_eq rfl : m=(m-m‚ÇÅ)+m‚ÇÅ) +
  cochain.comp (snd z) y‚ÇÇ (zero_add m).symm

lemma desc_cochain_eq {m m‚ÇÅ n‚ÇÅ : ‚Ñ§} (y‚ÇÅ : cochain F K m‚ÇÅ) (y‚ÇÇ : cochain G K m)
  (hm‚ÇÅ : m‚ÇÅ+1=n+m) (hn‚ÇÅ : n+n‚ÇÅ=1) : desc_cochain z y‚ÇÅ y‚ÇÇ hm‚ÇÅ =
cochain.comp ‚Üë(fst z hn‚ÇÅ) y‚ÇÅ (show m = n‚ÇÅ+m‚ÇÅ, begin
  suffices : m+1=n‚ÇÅ+m‚ÇÅ+1,
  { simpa only [add_left_inj] using this, },
  rw [add_assoc, hm‚ÇÅ, ‚Üê hn‚ÇÅ, add_comm n‚ÇÅ, add_comm n m, add_assoc],
end) + cochain.comp (snd z) y‚ÇÇ (zero_add m).symm :=
begin
  have h : n‚ÇÅ = m-m‚ÇÅ := by linarith,
  subst h,
  refl,
end

lemma inl_comp_desc_cochain {m m‚ÇÅ n‚ÇÄ : ‚Ñ§} (y‚ÇÅ : cochain F K m‚ÇÅ)
  (y‚ÇÇ : cochain G K m) (hm‚ÇÅ : m‚ÇÅ+1=n+m) (hn‚ÇÄ : n‚ÇÄ+1=n) :
  cochain.comp (inl z hn‚ÇÄ) (desc_cochain z y‚ÇÅ y‚ÇÇ hm‚ÇÅ) begin
    suffices : m‚ÇÅ+1 = n‚ÇÄ+m+1,
    { simpa only [add_left_inj] using this, },
    rw [add_assoc, hm‚ÇÅ, ‚Üê hn‚ÇÄ, add_assoc, add_comm 1 m],
  end = y‚ÇÅ :=
begin
  simp only [desc_cochain_eq z y‚ÇÅ y‚ÇÇ hm‚ÇÅ (show n+(-n‚ÇÄ)=1, by linarith), cochain.comp_add,
    ‚Üê cochain.comp_assoc (inl z hn‚ÇÄ) _ y‚ÇÅ (show 0=n‚ÇÄ+(-n‚ÇÄ), by linarith)
      (show m= _, by linarith) (show m‚ÇÅ=_, by linarith),
    ‚Üê cochain.comp_assoc_of_second_is_zero_cochain, add_zero,
    inl_comp_fst, inl_comp_snd, cochain.id_comp, cochain.zero_comp],
end

lemma inr_comp_desc_cochain {m m‚ÇÅ : ‚Ñ§} (y‚ÇÅ : cochain F K m‚ÇÅ)
  (y‚ÇÇ : cochain G K m) (hm‚ÇÅ : m‚ÇÅ+1=n+m) :
  cochain.comp (cochain.of_hom (inr z)) (desc_cochain z y‚ÇÅ y‚ÇÇ hm‚ÇÅ) (zero_add m).symm = y‚ÇÇ :=
begin
  simp only [desc_cochain_eq z y‚ÇÅ y‚ÇÇ hm‚ÇÅ (show n+(1-n)=1, by linarith), cochain.comp_add,
    ‚Üê cochain.comp_assoc_of_second_is_zero_cochain, inr_comp_snd, cochain.id_comp,
    ‚Üê cochain.comp_assoc_of_first_is_zero_cochain, inr_comp_fst, cochain.zero_comp, zero_add],
end

lemma Œ¥_desc_cochain {m m‚ÇÅ m‚ÇÇ n‚ÇÅ : ‚Ñ§} (y‚ÇÅ : cochain F K m‚ÇÅ) (y‚ÇÇ : cochain G K m)
  (hm‚ÇÅ : m‚ÇÅ+1=n+m) (hn‚ÇÅ : n+n‚ÇÅ=1) (hm‚ÇÇ : m‚ÇÅ+1=m‚ÇÇ)
  (m' : ‚Ñ§) (hm' : m+1=m') :
  Œ¥ m m' (desc_cochain z y‚ÇÅ y‚ÇÇ hm‚ÇÅ) =
  cochain.comp (fst z hn‚ÇÅ : cochain (twist z) F n‚ÇÅ) (Œ¥ m‚ÇÅ m‚ÇÇ y‚ÇÅ +
    Œµ (m+1) ‚Ä¢ cochain.comp ‚Üëz y‚ÇÇ (show m‚ÇÇ = n+m, by linarith)) (show m' = n‚ÇÅ+m‚ÇÇ, by linarith) +
  cochain.comp (snd z) (Œ¥ m m' y‚ÇÇ) (zero_add m').symm :=
begin
  simp only [desc_cochain_eq z y‚ÇÅ y‚ÇÇ hm‚ÇÅ hn‚ÇÅ, Œ¥_add, cochain.comp_add,
    Œ¥_comp_of_first_is_zero_cochain _ _ _ hm', Œ¥_snd z hn‚ÇÅ,
    Œ¥_comp ‚Üë(fst z hn‚ÇÅ) y‚ÇÅ (show m = n‚ÇÅ+m‚ÇÅ, by linarith) _ m‚ÇÇ m' hm' rfl hm‚ÇÇ,
    cochain.comp_zsmul, cochain.neg_comp, zsmul_neg, Œµ_add, Œµ_1, mul_neg, mul_one,
    neg_zsmul, cochain.comp_neg, cocycle.Œ¥_eq_zero, cochain.zero_comp, zsmul_zero, add_zero,
    add_assoc],
  rw cochain.comp_assoc _ _ _ (show 1=n‚ÇÅ+n, by linarith) (show m‚ÇÇ=n+m, by linarith)
    (show m' = n‚ÇÅ+n+m, by linarith),
  conv_rhs { congr, skip, rw add_comm, },
end

@[simps]
def desc_cocycle {m m‚ÇÅ n‚ÇÇ : ‚Ñ§} (y‚ÇÅ : cochain F K m‚ÇÅ) (y‚ÇÇ : cocycle G K m)
  (hm‚ÇÅ : m‚ÇÅ+1=n+m) (hn‚ÇÇ : n‚ÇÇ = n+m)
  (hy : Œ¥ m‚ÇÅ n‚ÇÇ y‚ÇÅ = Œµ m ‚Ä¢ cochain.comp (z : cochain F G n) (y‚ÇÇ : cochain G K m) hn‚ÇÇ) :
  cocycle (twist z) K m :=
cocycle.mk (desc_cochain z y‚ÇÅ ‚Üëy‚ÇÇ hm‚ÇÅ) _ rfl
begin
  simp only [Œ¥_desc_cochain z y‚ÇÅ ‚Üëy‚ÇÇ hm‚ÇÅ (show n+(1-n)=1, by linarith) (show m‚ÇÅ+1=n‚ÇÇ, by linarith) _ rfl,
    cocycle.Œ¥_eq_zero, cochain.comp_zero, add_zero, hy, Œµ_add, Œµ_1, mul_neg, mul_one, neg_zsmul,
    add_right_neg, cochain.comp_zero],
end

lemma inr_comp_desc_cocycle {m m‚ÇÅ n‚ÇÇ : ‚Ñ§} (y‚ÇÅ : cochain F K m‚ÇÅ) (y‚ÇÇ : cocycle G K m)
  (hm‚ÇÅ : m‚ÇÅ+1=n+m) (hn‚ÇÇ : n‚ÇÇ = n+m)
  (hy : Œ¥ m‚ÇÅ n‚ÇÇ y‚ÇÅ = Œµ m ‚Ä¢ cochain.comp (z : cochain F G n) (y‚ÇÇ : cochain G K m) hn‚ÇÇ) :
  cochain.comp (cochain.of_hom (inr z)) (desc_cocycle z y‚ÇÅ y‚ÇÇ hm‚ÇÅ hn‚ÇÇ hy : cochain (twist z) K m)
    (zero_add m).symm = y‚ÇÇ :=
by simp only [desc_cocycle, cocycle.mk_coe, inr_comp_desc_cochain]

@[simps]
def desc_hom_as_cocycle {m‚ÇÅ : ‚Ñ§} (y‚ÇÅ : cochain F K m‚ÇÅ) (y‚ÇÇ : G ‚ü∂ K)(hm‚ÇÅ : m‚ÇÅ+1=n)
  (hy : Œ¥ m‚ÇÅ n y‚ÇÅ = cochain.comp (z : cochain F G n) (cochain.of_hom y‚ÇÇ) (add_zero n).symm) :
  cocycle (twist z) K 0 :=
begin
  apply desc_cocycle z y‚ÇÅ (cocycle.of_hom y‚ÇÇ) (by linarith) (add_zero n).symm,
  simpa only [hy, Œµ_0, one_zsmul],
end

@[simps]
def desc {m‚ÇÅ : ‚Ñ§} (y‚ÇÅ : cochain F K m‚ÇÅ) (y‚ÇÇ : G ‚ü∂ K)
  (hm‚ÇÅ : m‚ÇÅ+1=n)
  (hy : Œ¥ m‚ÇÅ n y‚ÇÅ = cochain.comp (z : cochain F G n) (cochain.of_hom y‚ÇÇ) (add_zero n).symm) :
  twist z ‚ü∂ K :=
cocycle.hom_of (desc_hom_as_cocycle z y‚ÇÅ y‚ÇÇ hm‚ÇÅ hy)

@[simp]
lemma inr_comp_desc {m‚ÇÅ : ‚Ñ§} (y‚ÇÅ : cochain F K m‚ÇÅ) (y‚ÇÇ : G ‚ü∂ K)
  (hm‚ÇÅ : m‚ÇÅ+1=n)
  (hy : Œ¥ m‚ÇÅ n y‚ÇÅ = cochain.comp (z : cochain F G n) (cochain.of_hom y‚ÇÇ) (add_zero n).symm) :
  inr z ‚â´ desc z y‚ÇÅ y‚ÇÇ hm‚ÇÅ hy = y‚ÇÇ :=
begin
  apply (cocycle.equiv_hom G K).to_equiv.injective,
  ext1,
  dsimp [cocycle.equiv_hom],
  simp only [cocycle.of_hom, cocycle.mk_coe, cochain.of_hom_comp, desc,
    cocycle.cochain_of_hom_hom_of_eq_coe, desc_hom_as_cocycle_coe, inr_comp_desc_cochain],
end

def lift_cochain {m‚ÇÅ : ‚Ñ§} (y‚ÇÅ : cochain K F m‚ÇÅ) (y‚ÇÇ : cochain K G m)
  (hm : m+1=m‚ÇÅ+n) : cochain K (twist z) m :=
cochain.comp y‚ÇÅ (inl z (show (n-1)+1=n, by linarith)) (show m=m‚ÇÅ+(n-1), by linarith) +
  cochain.comp y‚ÇÇ (cochain.of_hom (inr z)) (add_zero m).symm

lemma lift_cochain_eq {m‚ÇÅ n‚ÇÄ : ‚Ñ§} (y‚ÇÅ : cochain K F m‚ÇÅ) (y‚ÇÇ : cochain K G m) (hm : m+1=m‚ÇÅ+n)
  (hn‚ÇÄ : n‚ÇÄ+1=n) : lift_cochain z y‚ÇÅ y‚ÇÇ hm =
cochain.comp y‚ÇÅ (inl z hn‚ÇÄ) (begin
  suffices : m+1=m‚ÇÅ+n‚ÇÄ+1,
  { simpa only [add_left_inj] using this, },
  rw [hm, ‚Üê hn‚ÇÄ, add_assoc],
end) + cochain.comp y‚ÇÇ (cochain.of_hom (inr z)) (add_zero m).symm :=
begin
  have eq : n‚ÇÄ = n-1 := by linarith,
  subst eq,
  refl,
end

@[simp]
lemma lift_cochain_comp_fst {m‚ÇÅ n‚ÇÅ : ‚Ñ§} (y‚ÇÅ : cochain K F m‚ÇÅ) (y‚ÇÇ : cochain K G m) (hm : m+1=m‚ÇÅ+n)
  (hn‚ÇÅ : n+n‚ÇÅ=1) : cochain.comp (lift_cochain z y‚ÇÅ y‚ÇÇ hm) ‚Üë(fst z hn‚ÇÅ)
    (show m‚ÇÅ=m+n‚ÇÅ, by { suffices : m‚ÇÅ+n = m+n‚ÇÅ+n,
    { simpa only [add_left_inj] using this,},
    rw [‚Üê hm, ‚Üê hn‚ÇÅ, add_comm n, add_assoc]}) = y‚ÇÅ :=
begin
  simp only [lift_cochain, cochain.add_comp,
    cochain.comp_assoc _ _ _ (show m=m‚ÇÅ+(n-1), by linarith) (show 0=n-1+n‚ÇÅ, by linarith)
    (show m‚ÇÅ=_, by linarith), inl_comp_fst, cochain.comp_id, add_zero,
    cochain.comp_assoc_of_second_is_zero_cochain, inr_comp_fst, cochain.comp_zero],
end

@[simp]
lemma lift_cochain_comp_snd {m‚ÇÅ : ‚Ñ§} (y‚ÇÅ : cochain K F m‚ÇÅ) (y‚ÇÇ : cochain K G m) (hm : m+1=m‚ÇÅ+n) :
  cochain.comp (lift_cochain z y‚ÇÅ y‚ÇÇ hm) (snd z) (add_zero m).symm = y‚ÇÇ :=
by simp only [lift_cochain, cochain.add_comp, cochain.comp_assoc_of_third_is_zero_cochain,
    inl_comp_snd, cochain.comp_zero, zero_add, inr_comp_snd, cochain.comp_id]

lemma Œ¥_lift_cochain {m‚ÇÅ n‚ÇÄ m‚ÇÇ : ‚Ñ§} (y‚ÇÅ : cochain K F m‚ÇÅ) (y‚ÇÇ : cochain K G m) (hm : m+1=m‚ÇÅ+n)
  (hn‚ÇÄ : n‚ÇÄ+1=n) (hm‚ÇÇ : m‚ÇÅ+1=m‚ÇÇ) (m' : ‚Ñ§) (hm' : m+1=m') :
  Œ¥ m m' (lift_cochain z y‚ÇÅ y‚ÇÇ hm) =
    Œµ n‚ÇÄ ‚Ä¢ cochain.comp (Œ¥ m‚ÇÅ m‚ÇÇ y‚ÇÅ) (inl z hn‚ÇÄ)
    (by rw [‚Üê hm', ‚Üê hm‚ÇÇ, hm, ‚Üê hn‚ÇÄ, add_comm n‚ÇÄ 1, add_assoc]) +
  cochain.comp (Œ¥ m m' y‚ÇÇ + cochain.comp y‚ÇÅ ‚Üëz (by rw [‚Üê hm', hm]))
    (cochain.of_hom (inr z)) (add_zero m').symm :=
begin
  simp only [lift_cochain_eq z y‚ÇÅ y‚ÇÇ hm hn‚ÇÄ, Œ¥_add,
    Œ¥_comp y‚ÇÅ (inl z hn‚ÇÄ) (show m = m‚ÇÅ+n‚ÇÄ, by linarith) m‚ÇÇ n m' hm' hm‚ÇÇ hn‚ÇÄ,
    Œ¥_comp_of_second_is_zero_cochain _ _ _ hm', Œ¥_inl, cocycle.Œ¥_cochain_of_hom,
    cochain.comp_zero, zero_add, cochain.comp_assoc_of_third_is_zero_cochain,
    cochain.add_comp],
  conv_lhs { rw [add_assoc, add_comm, add_assoc], },
end

def lift_cocycle {m‚ÇÅ n‚ÇÄ : ‚Ñ§} (y‚ÇÅ : cocycle K F m‚ÇÅ) (y‚ÇÇ : cochain K G m) (hm : m+1=m‚ÇÅ+n)
  (hn‚ÇÄ : n‚ÇÄ+1=n) (m' : ‚Ñ§) (hm' : m+1=m')
  (hy : Œ¥ m m' y‚ÇÇ + cochain.comp (y‚ÇÅ : cochain K F m‚ÇÅ) ‚Üëz (show m'=m‚ÇÅ+n, by rw [‚Üê hm', hm]) = 0) :
  cocycle K (twist z) m := cocycle.mk (lift_cochain z ‚Üëy‚ÇÅ y‚ÇÇ hm) m' hm'
(by simp only [Œ¥_lift_cochain z ‚Üëy‚ÇÅ y‚ÇÇ hm hn‚ÇÄ rfl m' hm', cocycle.Œ¥_eq_zero, cochain.zero_comp,
    zsmul_zero, zero_add, hy])

@[simps]
def lift_hom_as_cocycle {m‚ÇÅ n‚ÇÄ : ‚Ñ§} (y‚ÇÅ : cocycle K F m‚ÇÅ) (y‚ÇÇ : cochain K G 0) (hm : m‚ÇÅ+n=1)
  (hn‚ÇÄ : n‚ÇÄ+1=n)
  (hy : Œ¥ 0 1 y‚ÇÇ + cochain.comp (y‚ÇÅ : cochain K F m‚ÇÅ) ‚Üëz hm.symm = 0) : cocycle K (twist z) 0 :=
lift_cocycle z y‚ÇÅ y‚ÇÇ (show 0+1 = m‚ÇÅ+n, by linarith) hn‚ÇÄ 1 (zero_add 1) hy

@[simps]
def lift {m‚ÇÅ n‚ÇÄ : ‚Ñ§} (y‚ÇÅ : cocycle K F m‚ÇÅ) (y‚ÇÇ : cochain K G 0) (hm : m‚ÇÅ+n=1)
  (hn‚ÇÄ : n‚ÇÄ+1=n)
  (hy : Œ¥ 0 1 y‚ÇÇ + cochain.comp (y‚ÇÅ : cochain K F m‚ÇÅ) ‚Üëz hm.symm = 0) :
  K ‚ü∂ twist z :=
cocycle.hom_of (lift_hom_as_cocycle z y‚ÇÅ y‚ÇÇ hm hn‚ÇÄ hy)

lemma cochain_ext' (y‚ÇÅ y‚ÇÇ : cochain K (twist z) m) {n‚ÇÅ m‚ÇÅ : ‚Ñ§} (hn‚ÇÅ : n+n‚ÇÅ=1) (hm‚ÇÅ : m‚ÇÅ = m+n‚ÇÅ) :
  y‚ÇÅ = y‚ÇÇ ‚Üî cochain.comp y‚ÇÅ (fst z hn‚ÇÅ : cochain (twist z) F n‚ÇÅ) hm‚ÇÅ
      = cochain.comp y‚ÇÇ (fst z hn‚ÇÅ : cochain (twist z) F n‚ÇÅ) hm‚ÇÅ ‚àß
  cochain.comp y‚ÇÅ (snd z) (add_zero m).symm =
  cochain.comp y‚ÇÇ (snd z) (add_zero m).symm  := sorry

end twist

end hom_complex

end cochain_complex
