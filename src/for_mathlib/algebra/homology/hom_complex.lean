/-
Copyright (c) 2022 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/

import algebra.homology.homotopy
import algebra.homology.additive
import algebra.category.Group.abelian
import data.int.parity
import algebra.homology.short_exact.preadditive

noncomputable theory

open category_theory category_theory.preadditive category_theory.limits category_theory.category

universes v u

variables {C : Type u} [category.{v} C] [preadditive C]

namespace cochain_complex

variables {F G K L : cochain_complex C ‚Ñ§} (n m : ‚Ñ§)

namespace hom_complex

@[simp]
def Œµ (n : ‚Ñ§) : ‚Ñ§ := ‚Üë((-1 : units ‚Ñ§) ^ n)

@[simp]

lemma Œµ_add (n‚ÇÅ n‚ÇÇ : ‚Ñ§) : Œµ (n‚ÇÅ + n‚ÇÇ) = Œµ n‚ÇÅ ‚Ä¢ Œµ n‚ÇÇ :=
by { dsimp, rw [‚Üê units.coe_mul, ‚Üê units.ext_iff, zpow_add], }

@[simp]
lemma Œµ_0 : Œµ 0 = 1 := rfl

@[simp]
lemma Œµ_1 : Œµ 1 = -1 := rfl

@[simp]
lemma Œµ_succ (n : ‚Ñ§) : Œµ (n + 1) = - Œµ n :=
by simp only [Œµ_add, Œµ_1, algebra.id.smul_eq_mul, mul_neg, mul_one]

lemma Œµ_even (n : ‚Ñ§) (hn : even n) : Œµ n = 1 :=
begin
  change _ = ‚Üë(1 : units ‚Ñ§),
  cases hn with k hk,
  simp only [Œµ, ‚Üê units.ext_iff, hk, zpow_add, ‚Üê mul_zpow, int.units_mul_self],
end

lemma Œµ_odd (n : ‚Ñ§) (hn : odd n) : Œµ n = -1 :=
begin
  cases hn with k hk,
  rw [hk, Œµ_add, Œµ_1, Œµ_even (2*k) ‚ü®k, two_mul k‚ü©, one_zsmul],
end

structure triplet (n : ‚Ñ§) := (p : ‚Ñ§) (q : ‚Ñ§) (hpq : q=p+n)

variables (F G)

@[derive add_comm_group]
def cochain := Œ† (T : triplet n), F.X T.p ‚ü∂ G.X T.q

namespace cochain

variables {F G n}

def mk (v : Œ† (p q : ‚Ñ§) (hpq : q=p+n), F.X p ‚ü∂ G.X q) : cochain F G n :=
Œª T, v T.p T.q T.hpq

def v (c : cochain F G n) (p q : ‚Ñ§) (hpq : q=p+n) := c (triplet.mk p q hpq)

@[simp]
lemma zero_v {n : ‚Ñ§} (p q : ‚Ñ§) (hpq : q=p+n) : (0 : cochain F G n).v p q hpq = 0 := rfl

lemma mk_v (v : Œ† (p q : ‚Ñ§) (hpq : q=p+n), F.X p ‚ü∂ G.X q) (p q : ‚Ñ§) (hpq : q=p+n) :
  (mk v).v p q hpq = v p q hpq := rfl

lemma congr_v {z‚ÇÅ z‚ÇÇ : cochain F G n} (h : z‚ÇÅ = z‚ÇÇ) (p q : ‚Ñ§) (hpq : q=p+n) :
  z‚ÇÅ.v p q hpq = z‚ÇÇ.v p q hpq := by subst h

@[ext]
lemma ext (z‚ÇÅ z‚ÇÇ : cochain F G n) (h : ‚àÄ (p q : ‚Ñ§) (hpq : q=p+n), z‚ÇÅ.v p q hpq = z‚ÇÇ.v p q hpq) :
  z‚ÇÅ = z‚ÇÇ :=
begin
  ext T,
  rcases T with ‚ü®p, q, hpq‚ü©,
  exact h p q hpq,
end

@[ext]
lemma ext‚ÇÄ (z‚ÇÅ z‚ÇÇ : cochain F G 0)
  (h : ‚àÄ (p : ‚Ñ§), z‚ÇÅ.v p p (add_zero p).symm = z‚ÇÇ.v p p (add_zero p).symm ) : z‚ÇÅ = z‚ÇÇ :=
begin
  ext,
  have eq : q=p := by rw [hpq, add_zero],
  subst eq,
  apply h,
end

def of_homs (œà : Œ† (p : ‚Ñ§), F.X p ‚ü∂ G.X p) : cochain F G 0 :=
cochain.mk (Œª p q hpq, œà p ‚â´ eq_to_hom (by rw [hpq, add_zero]))

@[simp]
lemma of_homs_eq (œà : Œ† (p : ‚Ñ§), F.X p ‚ü∂ G.X p) (p : ‚Ñ§) :
  (of_homs œà).v p p (add_zero p).symm = œà p :=
by simp only [of_homs, mk_v, eq_to_hom_refl, comp_id]

def of_hom (œÜ : F ‚ü∂ G) : cochain F G 0 :=
of_homs (Œª p, œÜ.f p)

@[simp]
lemma of_hom_v (œÜ : F ‚ü∂ G) (p : ‚Ñ§) : (of_hom œÜ).v p p (add_zero p).symm = œÜ.f p :=
by simp only [of_hom, of_homs_eq]

@[simp]
def of_homotopy {œÜ‚ÇÅ œÜ‚ÇÇ : F ‚ü∂ G} (ho : homotopy œÜ‚ÇÅ œÜ‚ÇÇ) : cochain F G (-1) :=
cochain.mk (Œª p q hpq, ho.hom p q)

def comp {n‚ÇÅ n‚ÇÇ n‚ÇÅ‚ÇÇ : ‚Ñ§} (z‚ÇÅ : cochain F G n‚ÇÅ) (z‚ÇÇ : cochain G K n‚ÇÇ) (h : n‚ÇÅ‚ÇÇ = n‚ÇÅ + n‚ÇÇ) :
  cochain F K n‚ÇÅ‚ÇÇ :=
cochain.mk (Œª p q hpq, z‚ÇÅ.v p (p+n‚ÇÅ) rfl ‚â´ z‚ÇÇ.v (p+n‚ÇÅ) q (by linarith))

notation a ` ‚â´[` b `] ` c := cochain.comp a c b

lemma comp_v {n‚ÇÅ n‚ÇÇ n‚ÇÅ‚ÇÇ : ‚Ñ§} (z‚ÇÅ : cochain F G n‚ÇÅ) (z‚ÇÇ : cochain G K n‚ÇÇ) (h : n‚ÇÅ‚ÇÇ = n‚ÇÅ+n‚ÇÇ)
  (p‚ÇÅ p‚ÇÇ p‚ÇÉ : ‚Ñ§) (h‚ÇÅ : p‚ÇÇ=p‚ÇÅ+n‚ÇÅ) (h‚ÇÇ : p‚ÇÉ=p‚ÇÇ+n‚ÇÇ) :
  (comp z‚ÇÅ z‚ÇÇ h).v p‚ÇÅ p‚ÇÉ (by rw [h‚ÇÇ, h‚ÇÅ, h, add_assoc]) =
  z‚ÇÅ.v p‚ÇÅ p‚ÇÇ h‚ÇÅ ‚â´ z‚ÇÇ.v p‚ÇÇ p‚ÇÉ h‚ÇÇ := by { subst h‚ÇÅ, refl,}

@[simp]
lemma zero_comp {n‚ÇÅ n‚ÇÇ n‚ÇÅ‚ÇÇ : ‚Ñ§} (z‚ÇÇ : cochain G K n‚ÇÇ)
  (h : n‚ÇÅ‚ÇÇ = n‚ÇÅ + n‚ÇÇ) : comp (0 : cochain F G n‚ÇÅ) z‚ÇÇ h = 0 :=
begin
  ext,
  dsimp [comp, mk, v],
  simp only [zero_comp],
end

@[simp]
lemma add_comp {n‚ÇÅ n‚ÇÇ n‚ÇÅ‚ÇÇ : ‚Ñ§} (z‚ÇÅ z‚ÇÅ' : cochain F G n‚ÇÅ) (z‚ÇÇ : cochain G K n‚ÇÇ)
  (h : n‚ÇÅ‚ÇÇ = n‚ÇÅ + n‚ÇÇ) : comp (z‚ÇÅ+z‚ÇÅ') z‚ÇÇ h = comp z‚ÇÅ z‚ÇÇ h + comp z‚ÇÅ' z‚ÇÇ h :=
begin
  ext,
  dsimp [comp, mk, v],
  simp only [add_comp],
end

@[simp]
lemma sub_comp {n‚ÇÅ n‚ÇÇ n‚ÇÅ‚ÇÇ : ‚Ñ§} (z‚ÇÅ z‚ÇÅ' : cochain F G n‚ÇÅ) (z‚ÇÇ : cochain G K n‚ÇÇ)
  (h : n‚ÇÅ‚ÇÇ = n‚ÇÅ + n‚ÇÇ) : comp (z‚ÇÅ-z‚ÇÅ') z‚ÇÇ h = comp z‚ÇÅ z‚ÇÇ h - comp z‚ÇÅ' z‚ÇÇ h :=
begin
  ext,
  dsimp [comp, mk, v],
  simp only [sub_comp],
end

@[simp]
lemma neg_comp {n‚ÇÅ n‚ÇÇ n‚ÇÅ‚ÇÇ : ‚Ñ§} (z‚ÇÅ : cochain F G n‚ÇÅ) (z‚ÇÇ : cochain G K n‚ÇÇ)
  (h : n‚ÇÅ‚ÇÇ = n‚ÇÅ + n‚ÇÇ) : comp (-z‚ÇÅ) z‚ÇÇ h = -comp z‚ÇÅ z‚ÇÇ h :=
begin
  ext,
  dsimp [comp, mk, v],
  simp only [neg_comp],
end

@[simp]
lemma zsmul_comp {n‚ÇÅ n‚ÇÇ n‚ÇÅ‚ÇÇ : ‚Ñ§} (k : ‚Ñ§) (z‚ÇÅ : cochain F G n‚ÇÅ) (z‚ÇÇ : cochain G K n‚ÇÇ)
  (h : n‚ÇÅ‚ÇÇ = n‚ÇÅ + n‚ÇÇ) : comp (k ‚Ä¢ z‚ÇÅ) z‚ÇÇ h = k ‚Ä¢ comp z‚ÇÅ z‚ÇÇ h :=
begin
  ext,
  dsimp [comp, mk, v],
  simp only [zsmul_comp],
end

@[simp]
lemma zero_cochain_comp {n : ‚Ñ§} (z‚ÇÅ : cochain F G 0) (z‚ÇÇ : cochain G K n)
  (p q : ‚Ñ§) (hpq : q=p+n) :
  (cochain.comp z‚ÇÅ z‚ÇÇ (zero_add n).symm).v p q hpq =
    z‚ÇÅ.v p p (add_zero p).symm ‚â´ z‚ÇÇ.v p q hpq :=
comp_v z‚ÇÅ z‚ÇÇ (zero_add n).symm p p q (add_zero p).symm hpq

@[simp]
lemma id_comp {n : ‚Ñ§} (z‚ÇÇ : cochain F G n) :
  cochain.comp (cochain.of_hom (ùüô F)) z‚ÇÇ (zero_add n).symm = z‚ÇÇ :=
begin
  ext,
  simp only [zero_cochain_comp, of_hom_v, homological_complex.id_f, id_comp],
end

@[simp]
lemma comp_zero {n‚ÇÅ n‚ÇÇ n‚ÇÅ‚ÇÇ : ‚Ñ§} (z‚ÇÅ : cochain F G n‚ÇÅ)
  (h : n‚ÇÅ‚ÇÇ = n‚ÇÅ + n‚ÇÇ) : comp z‚ÇÅ (0 : cochain G K n‚ÇÇ) h = 0 :=
begin
  ext,
  dsimp [comp, mk, v],
  simp only [comp_zero],
end

@[simp]
lemma comp_add {n‚ÇÅ n‚ÇÇ n‚ÇÅ‚ÇÇ : ‚Ñ§} (z‚ÇÅ : cochain F G n‚ÇÅ) (z‚ÇÇ z‚ÇÇ' : cochain G K n‚ÇÇ)
  (h : n‚ÇÅ‚ÇÇ = n‚ÇÅ + n‚ÇÇ) : comp z‚ÇÅ (z‚ÇÇ+z‚ÇÇ') h = comp z‚ÇÅ z‚ÇÇ h + comp z‚ÇÅ z‚ÇÇ' h :=
begin
  ext,
  dsimp [comp, mk, v],
  simp only [comp_add],
end

@[simp]
lemma comp_sub {n‚ÇÅ n‚ÇÇ n‚ÇÅ‚ÇÇ : ‚Ñ§} (z‚ÇÅ : cochain F G n‚ÇÅ) (z‚ÇÇ z‚ÇÇ' : cochain G K n‚ÇÇ)
  (h : n‚ÇÅ‚ÇÇ = n‚ÇÅ + n‚ÇÇ) : comp z‚ÇÅ (z‚ÇÇ-z‚ÇÇ') h = comp z‚ÇÅ z‚ÇÇ h - comp z‚ÇÅ z‚ÇÇ' h :=
begin
  ext,
  dsimp [comp, mk, v],
  simp only [comp_sub],
end

@[simp]
lemma comp_neg {n‚ÇÅ n‚ÇÇ n‚ÇÅ‚ÇÇ : ‚Ñ§} (z‚ÇÅ : cochain F G n‚ÇÅ) (z‚ÇÇ : cochain G K n‚ÇÇ)
  (h : n‚ÇÅ‚ÇÇ = n‚ÇÅ + n‚ÇÇ) : comp z‚ÇÅ (-z‚ÇÇ) h = -comp z‚ÇÅ z‚ÇÇ h :=
begin
  ext,
  dsimp [comp, mk, v],
  simp only [comp_neg],
end

@[simp]
lemma comp_zsmul {n‚ÇÅ n‚ÇÇ n‚ÇÅ‚ÇÇ : ‚Ñ§} (k : ‚Ñ§) (z‚ÇÅ : cochain F G n‚ÇÅ) (z‚ÇÇ : cochain G K n‚ÇÇ)
  (h : n‚ÇÅ‚ÇÇ = n‚ÇÅ + n‚ÇÇ) : comp z‚ÇÅ (k ‚Ä¢ z‚ÇÇ) h = k ‚Ä¢ comp z‚ÇÅ z‚ÇÇ h :=
begin
  ext,
  dsimp [comp, mk, v],
  simp only [comp_zsmul],
end

@[simp]
lemma comp_zero_cochain {n : ‚Ñ§} (z‚ÇÅ : cochain F G n) (z‚ÇÇ : cochain G K 0)
  (p q : ‚Ñ§) (hpq : q=p+n) :
  (cochain.comp z‚ÇÅ z‚ÇÇ (add_zero n).symm).v p q hpq =
    z‚ÇÅ.v p q hpq ‚â´ z‚ÇÇ.v q q (add_zero q).symm :=
comp_v z‚ÇÅ z‚ÇÇ (add_zero n).symm p q q hpq (add_zero q).symm

@[simp]
lemma comp_id {n : ‚Ñ§} (z‚ÇÅ : cochain F G n) :
  cochain.comp z‚ÇÅ (cochain.of_hom (ùüô G)) (add_zero n).symm = z‚ÇÅ :=
begin
  ext,
  simp only [comp_zero_cochain, of_hom_v, homological_complex.id_f, comp_id],
end

@[simp]
lemma cochain_of_hom_comp (f : F ‚ü∂ G) (g : G ‚ü∂ K) :
  cochain.comp (cochain.of_hom f) (cochain.of_hom g) (zero_add 0).symm =
  cochain.of_hom (f ‚â´ g) :=
begin
  ext,
  simp only [comp_zero_cochain, of_hom_v, homological_complex.comp_f],
end

lemma comp_assoc {n‚ÇÅ n‚ÇÇ n‚ÇÉ n‚ÇÅ‚ÇÇ n‚ÇÇ‚ÇÉ n‚ÇÅ‚ÇÇ‚ÇÉ : ‚Ñ§}
  (z‚ÇÅ : cochain F G n‚ÇÅ) (z‚ÇÇ : cochain G K n‚ÇÇ) (z‚ÇÉ : cochain K L n‚ÇÉ)
  (h‚ÇÅ‚ÇÇ : n‚ÇÅ‚ÇÇ = n‚ÇÅ + n‚ÇÇ) (h‚ÇÇ‚ÇÉ : n‚ÇÇ‚ÇÉ = n‚ÇÇ + n‚ÇÉ) (h‚ÇÅ‚ÇÇ‚ÇÉ : n‚ÇÅ‚ÇÇ‚ÇÉ = n‚ÇÅ + n‚ÇÇ + n‚ÇÉ) :
  cochain.comp (cochain.comp z‚ÇÅ z‚ÇÇ h‚ÇÅ‚ÇÇ) z‚ÇÉ (show n‚ÇÅ‚ÇÇ‚ÇÉ = n‚ÇÅ‚ÇÇ + n‚ÇÉ, by rw [h‚ÇÅ‚ÇÇ, h‚ÇÅ‚ÇÇ‚ÇÉ]) =
    cochain.comp z‚ÇÅ (cochain.comp z‚ÇÇ z‚ÇÉ h‚ÇÇ‚ÇÉ)
      (show n‚ÇÅ‚ÇÇ‚ÇÉ = n‚ÇÅ + n‚ÇÇ‚ÇÉ, by rw [h‚ÇÇ‚ÇÉ, h‚ÇÅ‚ÇÇ‚ÇÉ, add_assoc]) :=
begin
  ext,
  simp only [comp_v _ _ (show n‚ÇÅ‚ÇÇ‚ÇÉ = n‚ÇÅ‚ÇÇ + n‚ÇÉ, by rw [h‚ÇÅ‚ÇÇ, h‚ÇÅ‚ÇÇ‚ÇÉ]) p (p+n‚ÇÅ‚ÇÇ) q rfl (by linarith),
    comp_v _ _ h‚ÇÅ‚ÇÇ p (p+n‚ÇÅ) (p+n‚ÇÅ‚ÇÇ) rfl (by linarith),
    comp_v z‚ÇÅ (cochain.comp z‚ÇÇ z‚ÇÉ h‚ÇÇ‚ÇÉ) (show n‚ÇÅ‚ÇÇ‚ÇÉ = n‚ÇÅ + n‚ÇÇ‚ÇÉ, by linarith)
      p (p+n‚ÇÅ) q rfl (by linarith),
    comp_v _ _ h‚ÇÇ‚ÇÉ (p+n‚ÇÅ) (p+n‚ÇÅ‚ÇÇ) q (by linarith) (by linarith), assoc],
end

@[simp]
lemma comp_assoc_of_first_is_zero_cochain {n‚ÇÇ n‚ÇÉ n‚ÇÇ‚ÇÉ : ‚Ñ§}
  (z‚ÇÅ : cochain F G 0) (z‚ÇÇ : cochain G K n‚ÇÇ) (z‚ÇÉ : cochain K L n‚ÇÉ)
  (h‚ÇÇ‚ÇÉ : n‚ÇÇ‚ÇÉ = n‚ÇÇ + n‚ÇÉ) :
  cochain.comp (cochain.comp z‚ÇÅ z‚ÇÇ (zero_add n‚ÇÇ).symm) z‚ÇÉ h‚ÇÇ‚ÇÉ =
    cochain.comp z‚ÇÅ (cochain.comp z‚ÇÇ z‚ÇÉ h‚ÇÇ‚ÇÉ)
      (zero_add n‚ÇÇ‚ÇÉ).symm :=
comp_assoc z‚ÇÅ z‚ÇÇ z‚ÇÉ (zero_add n‚ÇÇ).symm h‚ÇÇ‚ÇÉ (by linarith)

@[simp]
lemma comp_assoc_of_second_is_zero_cochain {n‚ÇÅ n‚ÇÉ n‚ÇÅ‚ÇÉ : ‚Ñ§}
  (z‚ÇÅ : cochain F G n‚ÇÅ) (z‚ÇÇ : cochain G K 0) (z‚ÇÉ : cochain K L n‚ÇÉ) (h‚ÇÅ‚ÇÉ : n‚ÇÅ‚ÇÉ = n‚ÇÅ + n‚ÇÉ) :
  cochain.comp (cochain.comp z‚ÇÅ z‚ÇÇ (add_zero n‚ÇÅ).symm) z‚ÇÉ h‚ÇÅ‚ÇÉ =
    cochain.comp z‚ÇÅ (cochain.comp z‚ÇÇ z‚ÇÉ (zero_add n‚ÇÉ).symm) h‚ÇÅ‚ÇÉ :=
comp_assoc z‚ÇÅ z‚ÇÇ z‚ÇÉ (add_zero n‚ÇÅ).symm (zero_add n‚ÇÉ).symm (by linarith)

@[simp]
lemma comp_assoc_of_third_is_zero_cochain {n‚ÇÅ n‚ÇÇ n‚ÇÅ‚ÇÇ : ‚Ñ§}
  (z‚ÇÅ : cochain F G n‚ÇÅ) (z‚ÇÇ : cochain G K n‚ÇÇ) (z‚ÇÉ : cochain K L 0) (h‚ÇÅ‚ÇÇ : n‚ÇÅ‚ÇÇ = n‚ÇÅ + n‚ÇÇ) :
  cochain.comp (cochain.comp z‚ÇÅ z‚ÇÇ h‚ÇÅ‚ÇÇ) z‚ÇÉ (add_zero n‚ÇÅ‚ÇÇ).symm =
    cochain.comp z‚ÇÅ (cochain.comp z‚ÇÇ z‚ÇÉ (add_zero n‚ÇÇ).symm) h‚ÇÅ‚ÇÇ :=
comp_assoc z‚ÇÅ z‚ÇÇ z‚ÇÉ h‚ÇÅ‚ÇÇ (add_zero n‚ÇÇ).symm (by linarith)

end cochain

/- Differentials -/

variables {F G} (n)

def Œ¥ (z : cochain F G n) : cochain F G m :=
cochain.mk (Œª (p q : ‚Ñ§) hpq, z.v p (p+n) rfl ‚â´ G.d (p+n) q +
  Œµ (n+1) ‚Ä¢ F.d p (p+m-n) ‚â´ z.v (p+m-n) q (by { dsimp [int.sub], linarith}))

lemma Œ¥_v (z : cochain F G n) (p q : ‚Ñ§) (hpq : q=p+m)  (hnm : n+1=m) (q‚ÇÅ q‚ÇÇ : ‚Ñ§)
  (hq‚ÇÅ : q‚ÇÅ=q-1) (hq‚ÇÇ : q‚ÇÇ=p+1) : (Œ¥ n m z).v p q hpq =
  z.v p q‚ÇÅ (by {rw [hq‚ÇÅ, hpq, ‚Üê hnm, ‚Üê add_assoc, add_tsub_cancel_right],}) ‚â´ G.d q‚ÇÅ q
  + Œµ (n+1) ‚Ä¢ F.d p q‚ÇÇ ‚â´ z.v q‚ÇÇ q (by rw [hpq, hq‚ÇÇ, ‚Üê hnm, add_comm n, add_assoc]) :=
begin
  have h‚ÇÅ : q‚ÇÅ = p+n := by linarith,
  have h‚ÇÇ : q‚ÇÇ = p+m-n := by linarith,
  substs h‚ÇÅ h‚ÇÇ,
  refl,
end

lemma Œ¥_shape (hnm : ¬¨ n+1=m) (z : cochain F G n) : Œ¥ n m z = 0 :=
begin
  ext,
  dsimp [Œ¥, cochain.mk, cochain.v],
  rw [F.shape, G.shape, limits.comp_zero, limits.zero_comp, smul_zero, add_zero],
  all_goals
  { change ¬¨ _=_ ,
    intro h,
    apply hnm,
    linarith, },
end

variables (F G)

def Œ¥_hom : cochain F G n ‚Üí+ cochain F G m :=
{ to_fun := Œ¥ n m,
  map_zero' := begin
    ext,
    dsimp [Œ¥, cochain.mk, cochain.v],
    simp only [limits.zero_comp, limits.comp_zero, smul_zero, add_zero],
  end,
  map_add' := Œª z‚ÇÅ z‚ÇÇ, begin
    ext,
    dsimp [Œ¥, cochain.mk, cochain.v],
    simp only [preadditive.add_comp, preadditive.comp_add, smul_add],
    abel,
  end}

variables {F G}

@[simp]
lemma Œ¥_add (z‚ÇÅ z‚ÇÇ : cochain F G n) : Œ¥ n m (z‚ÇÅ + z‚ÇÇ) = Œ¥ n m z‚ÇÅ + Œ¥ n m z‚ÇÇ :=
(Œ¥_hom F G n m).map_add z‚ÇÅ z‚ÇÇ

@[simp]
lemma Œ¥_sub (z‚ÇÅ z‚ÇÇ : cochain F G n) : Œ¥ n m (z‚ÇÅ - z‚ÇÇ) = Œ¥ n m z‚ÇÅ - Œ¥ n m z‚ÇÇ :=
(Œ¥_hom F G n m).map_sub z‚ÇÅ z‚ÇÇ

@[simp]
lemma Œ¥_zero : Œ¥ n m (0 : cochain F G n) = 0 := (Œ¥_hom F G n m).map_zero

@[simp]
lemma Œ¥_neg (z : cochain F G n) : Œ¥ n m (-z) = - Œ¥ n m z :=
(Œ¥_hom F G n m).map_neg z

@[simp]
lemma Œ¥_zsmul (k : ‚Ñ§) (z : cochain F G n) : Œ¥ n m (k ‚Ä¢ z) = k ‚Ä¢ Œ¥ n m z :=
(Œ¥_hom F G n m).map_zsmul z k

@[simp]
lemma Œ¥Œ¥ (n‚ÇÄ n‚ÇÅ n‚ÇÇ : ‚Ñ§) (z : cochain F G n‚ÇÄ) : Œ¥ n‚ÇÅ n‚ÇÇ (Œ¥ n‚ÇÄ n‚ÇÅ z) = 0 :=
begin
  by_cases h‚ÇÄ‚ÇÅ : n‚ÇÄ+1 = n‚ÇÅ, swap,
  { rw [Œ¥_shape n‚ÇÄ n‚ÇÅ h‚ÇÄ‚ÇÅ, Œ¥_zero], },
  by_cases h‚ÇÅ‚ÇÇ : n‚ÇÅ+1 = n‚ÇÇ, swap,
  { rw [Œ¥_shape n‚ÇÅ n‚ÇÇ h‚ÇÅ‚ÇÇ], },
  ext,
  rw Œ¥_v n‚ÇÅ n‚ÇÇ _ p q hpq h‚ÇÅ‚ÇÇ _ _ rfl rfl,
  rw Œ¥_v n‚ÇÄ n‚ÇÅ z p (q-1) (by linarith) h‚ÇÄ‚ÇÅ (q-2) _ (by linarith) rfl,
  rw Œ¥_v n‚ÇÄ n‚ÇÅ z (p+1) q (by linarith) h‚ÇÄ‚ÇÅ _ (p+2) rfl (by linarith),
  simp only [‚Üê h‚ÇÄ‚ÇÅ, Œµ_succ, add_comp, neg_neg, neg_zsmul, neg_comp, cochain.zero_v,
    zsmul_comp, comp_zsmul, comp_add, comp_neg, assoc, homological_complex.d_comp_d,
    homological_complex.d_comp_d_assoc, comp_zero, zero_comp, zsmul_zero, neg_zero, add_zero,
    zero_add, add_left_neg],
end

end hom_complex

variables (F G)

open hom_complex

def hom_complex : cochain_complex AddCommGroup ‚Ñ§ :=
{ X := Œª i, AddCommGroup.of (cochain F G i),
  d := Œª i j, AddCommGroup.of_hom (Œ¥_hom F G i j),
  shape' := Œª i j hij, by { ext1 z, exact Œ¥_shape i j hij z, },
  d_comp_d' := Œª i j k hij hjk, by { ext1 f, apply Œ¥Œ¥, } }

namespace hom_complex

def cocycle : add_subgroup (cochain F G n) :=
add_monoid_hom.ker ((hom_complex F G).d n (n+1))

namespace cocycle

variables {F G}

def mem_iff (hnm : n+1=m) (z : cochain F G n) :
  z ‚àà cocycle F G n ‚Üî Œ¥ n m z = 0 :=
by { subst hnm, refl, }

variable {n}

@[simps]
def mk (z : cochain F G n) (m : ‚Ñ§) (hnm : n+1 = m) (h : Œ¥ n m z = 0) : cocycle F G n :=
‚ü®z, by simpa only [mem_iff n m hnm z] using h‚ü©

@[simp]
lemma Œ¥_eq_zero {n : ‚Ñ§} (z : cocycle F G n) (m : ‚Ñ§) : Œ¥ n m (z : cochain F G n) = 0 :=
begin
  by_cases n+1=m,
  { rw ‚Üê mem_iff n m h,
    exact z.2, },
  { apply Œ¥_shape n m h, }
end

def of_hom (œÜ : F ‚ü∂ G) : cocycle F G 0 := mk (cochain.of_hom œÜ) 1 (zero_add 1)
begin
  ext,
  simp only [Œ¥_v 0 1 _ p q hpq (zero_add 1) p q (by linarith) hpq,
    cochain.of_hom_v, homological_complex.hom.comm, Œµ_1, neg_smul, one_zsmul,
    add_right_neg, cochain.zero_v, zero_add],
end

@[simp]
lemma Œ¥_cochain_of_hom (œÜ : F ‚ü∂ G) : Œ¥ 0 1 (cochain.of_hom œÜ) = 0 :=
by apply Œ¥_eq_zero (of_hom œÜ)

@[simps]
def hom_of (z : cocycle F G 0) : F ‚ü∂ G :=
{ f := Œª i, (z : cochain F G 0).v i i (add_zero i).symm,
  comm' := Œª i j hij, begin
    change i+1=j at hij,
    have hz‚ÇÅ := z.2,
    rw mem_iff 0 1 (zero_add 1) at hz‚ÇÅ,
    simpa only [Œ¥_v 0 1 z.1 i j hij.symm (zero_add 1) i j (by linarith) hij.symm,
      zero_add, Œµ_1, neg_smul, one_zsmul, cochain.zero_v, add_neg_eq_zero]
      using cochain.congr_v hz‚ÇÅ i j hij.symm,
  end, }

@[simp]
lemma hom_of_of_hom_eq_self (œÜ : F ‚ü∂ G) : hom_of (of_hom œÜ) = œÜ :=
by { ext i, simp only [of_hom, hom_of_f, mk_coe, cochain.of_hom_v], }

@[simp]
lemma of_hom_hom_of_eq_self (z : cocycle F G 0) : of_hom (hom_of z) = z :=
begin
  ext,
  simp only [of_hom, mk_coe, cochain.of_hom_v, hom_of_f],
end

@[simp]
lemma coe_of_hom_hom_of_eq_coe (z : cocycle F G 0) : (of_hom (hom_of z) : cochain F G 0) = z :=
by rw of_hom_hom_of_eq_self z

variables (F G)

def equiv_hom : (F ‚ü∂ G) ‚âÉ+ cocycle F G 0 :=
{ to_fun := of_hom,
  inv_fun := hom_of,
  left_inv := hom_of_of_hom_eq_self,
  right_inv := of_hom_hom_of_eq_self,
  map_add' := Œª œÜ‚ÇÅ œÜ‚ÇÇ, begin
    ext,
    simp only [of_hom, cochain.of_hom, cochain.of_homs, cochain.mk, cochain.v,
      homological_complex.add_f_apply, mk_coe, eq_to_hom_refl, comp_id,
      add_subgroup.coe_add, pi.add_apply],
  end, }

end cocycle

end hom_complex

end cochain_complex
